<PACKAGE "M-RDR">

<ENTRY READER-MAIL READER-DELIVERY READER-CITATION>

<USE "M-DEFS" "FIELDS" "USRUTI" "M-TEMP" "M-READ" "R-DEFS" "MADMAN" "ASYLUM">

<SETG COMSYS-READER-ASYLUM <>>

"READER-MAIL -- deliver citation to file READER.TXT on user's directory"

<DEFINE READER-MAIL (ADR "AUX" CH (MS ,MSG-SPACE) U UU ID)
	#DECL ((ADR) STRING (U UU) <OR FALSE STRING> (CH) <OR CHANNEL FALSE>
	       (ID) FIX (MS) SPACE)
	<COND (<SET U <DRDMSG "" <GETFIX "FROM">>>
	       <SET UU <KEYGET <COND (<=? .ADR ""> .U) (ELSE .ADR)>>>
	       <COND (<SET CH <MAIL-OPEN .UU "READER">>
		      <SET ID <DRDMSG "" "MSG-NO">>
		      <COND (<AND <=? .ADR ""> <NOT <DRDMSG "" "REMIND">>>
			     <SET ID <- .ID>>)>
		      <PRINT .ID .CH>
		      <TEMPRENAME .CH>
		      T)
		     (ELSE <AFALSE 7 <ASTRING .MS <UNPARSE .CH>>>)>)
	      (ELSE <AFALSE 8 <ASTRING .MS "Author">>)>>

"READER-CITATION -- deliver citation to user's asylum"

<DEFINE READER-CITATION (ADR "AUX" RA (SS ,SCRATCH-SPACE) (MS ,MSG-SPACE)
			 U UU ID R (N <>) (NN <>))
	#DECL ((ADR) STRING (RA) <OR FALSE ASYLUM> (SS) SPACE
	       (R) <OR FALSE MANIAC> (N NN) <OR FALSE UVECTOR>
	       (UU) <OR FALSE STRING>)
	<COND (<AND <SET U <DRDMSG "" <GETFIX "FROM">>>
		    <SET UU <KEYGET <COND (<=? .ADR ""> .U) (ELSE .ADR)>>>>
	       <SET RA
		    <OPEN-DATA-FILE <STRING "<" .UU ">READER.ASYLUM">
				    ,COMSYS-READER-ASYLUM>>
	       <SETG COMSYS-READER-ASYLUM .RA>
	       <SET ID <DRDMSG "" "MSG-NO">>
	       <COND (<AND <=? .ADR ""> <NOT <DRDMSG "" "REMIND">>>
		      <SET ID <- .ID>>)>
	       <SET N <DATA-AREAD .RA ,M-NEW <ARESET .SS>>>
	       <COND (.N
		      <SET NN <AUVECTOR .SS !.N .ID>>
		      <ARELEASE .SS .N>)
		     (ELSE
		      <SET NN <AUVECTOR .SS .ID>>)>
	       <COND (<SET R <DATA-APRINT .RA ,M-NEW .SS .NN>>
		      <FLUSH-DATA-FILE .RA>
		      T)
		     (ELSE <AFALSE 36 <ACOPY .MS .R>>)>)
	      (ELSE <AFALSE 35>)>>

"READER-DELIVERY -- deliver citation and message to user's asylum"

<DEFINE READER-DELIVERY (ADR "AUX" RA (MS ,MSG-SPACE) (SS ,SCRATCH-SPACE)
			 LC U UU ID R RR (V <>) (VV <>) (N <>) (NN <>)
			 (M -1) TMP)
	#DECL ((ADR) STRING (RA) <OR FALSE ASYLUM> (MS SS) SPACE
	       (RR R) <OR FALSE MANIAC> (UU) <OR FALSE STRING> (M LC) FIX
	       (TMP) <OR FALSE FIX> (V VV) <OR FALSE VECTOR>
	       (N NN) <OR FALSE UVECTOR>)
	<COND (<AND <SET U <DRDMSG "" <GETFIX "FROM">>>
		    <SET UU <KEYGET <COND (<=? .ADR ""> .U) (ELSE .ADR)>>>>
	       <SET RA
		    <OPEN-DATA-FILE <STRING "<" .UU ">READER.ASYLUM">
				    ,COMSYS-READER-ASYLUM>>
	       <SETG COMSYS-READER-ASYLUM .RA>
	       <SET ID <DRDMSG "" "MSG-NO">>
	       <COND (<AND <SET V <DATA-AREAD .RA ,M-ASYLUM <ARESET .SS>>>
			   <SET TMP <FIND-LOCAL .ID .V>>>
		      <SET M .TMP>)>
	       <COND (<NOT <SET R <DATA-OPEN "PRINT" .RA .M>>>
		      <AFALSE 30 <ACOPY .MS .R>>)
		     (<L? <SET LC <1 .R>> 10>
		      <DATA-CLOSE .RA .R>
		      <AFALSE 31
			      <ASTRING .MS "ABOUT-TO-CLOBBER"> <1 .R>>)
		     (<NOT <SET RR <DATA-IPRINT .RA .R .MS <MVDATA ,MDB>>>>
		      <DATA-CLOSE .RA .R>
		      <AFALSE 32 <ACOPY .MS .RR>>)
		     (<NOT <SET R <DATA-CLOSE .RA .R>>>
		      <AFALSE 33 <ACOPY .MS .R>>)
		     (ELSE
		      <COND (<AND <=? .ADR ""> <NOT <DRDMSG "" "REMIND">>>
			     <SET ID <- .ID>>)>
		      <COND (.V
			     <SET VV <AVECTOR .SS !.V .ID .LC>>
			     <ARELEASE .SS .V>)
			    (ELSE
			     <SET VV <AVECTOR .SS .ID .LC>>)>
		      <COND (<SET R <DATA-APRINT .RA ,M-ASYLUM .SS .VV>>
			     <SET N <DATA-AREAD .RA ,M-NEW <ARESET .SS>>>
			     <COND (.N
				    <SET NN <AUVECTOR .SS !.N .ID>>
				    <ARELEASE .SS .N>)
				   (ELSE
				    <SET NN <AUVECTOR .SS .ID>>)>
			     <COND (<SET R
					 <DATA-APRINT .RA ,M-NEW .SS .NN>>
				    <FLUSH-DATA-FILE .RA>
				    T)
				   (ELSE <AFALSE 36 <ACOPY .MS .R>>)>)
			    (ELSE <AFALSE 34 <ACOPY .MS .R>>)>)>)
	      (ELSE <AFALSE 35>)>>

<DEFINE FIND-LOCAL (ID V)
	#DECL ((ID) FIX (V) <VECTOR [REST FIX]>)
	<REPEAT ()
		<COND (<EMPTY? .V> <RETURN <>>)
		      (<==? <1 .V> .ID> <RETURN <2 .V>>)
		      (ELSE <SET V <REST .V 2>>)>>>

<ENDPACKAGE>

<PACKAGE "M-HIST">

<ENTRY HISTORY PROCESSES>

<USE "M-READ" "M-DEFS" "ITIME" "FIELDS">

<DEFINE HISTORY (N "OPTIONAL" (P "DELIVERY")
		 "AUX" D (OUTCHAN .OUTCHAN))
	#DECL ((N O) FIX (A P) STRING
	       (OUTCHAN) CHANNEL (D) <LIST [REST STRING]>)
	<COND (<MRD .N>
	       <SET D <DUSERS>>
	       <COND (<=? .P "*">
		      <MAPF <>
			    <FUNCTION (A)
				 #DECL ((A) STRING)
				 <COND (<=? .A ""> <PRINC "[Author]">)
				       (ELSE <PRINC .A>)>
				 <CRLF>
				 <MAPF <>
				       <FUNCTION (P)
					    #DECL ((P) STRING)
					    <PRC-INFO .A .P <>>>
				       ,PROCESSES>
				 <CRLF>>
			    <DUSERS>>)
		     (<MEMBER .P '["DELIVERY" "RECEIVER-PROCESSING"]>
		      <MAPF <>
			    <FUNCTION (A)
				 #DECL ((A) STRING)
				 <COND (<N=? .A "">
					<PRINC .A>
					<CRLF>
					<PRC-INFO .A .P <>>
					<CRLF>)>>
			    <DUSERS>>)
		     (ELSE
		      <PRINC "[Author]">
		      <CRLF>
		      <PRC-INFO "" .P>)>
	       .N)>>

<SETG PROCESSES ["SENDING"
		 "PARSING"
		 "EXPANSION"
		 "TRANSMISSION"
		 "PERSONAL-PROCESSING"
		 "AUTHOR-PROCESSING"
		 "RECEIVER-PROCESSING"
		 "FILE-OUTPUT"
		 "DELIVERY"
		 "ACKNOWLEDGMENT"
		 "CORRECTION"]>

<GDECL (PROCESSES) <VECTOR [REST STRING]>>

<DEFINE PRC-INFO (A P "OPTIONAL" (V? T) "AUX" PP EC PC PN (OUTCHAN .OUTCHAN))
	#DECL ((A P) STRING (PP) <OR FALSE LIST> (V?) <OR ATOM FALSE>
	       (EC PC PN) <OR FALSE <LIST STRING <OR FALSE LIST FIX>>>
	       (OUTCHAN) CHANNEL)
	<SET EC <AND <SET PP <DRDMSG .A <GETFIX "ERROR-CONDITIONS">>>
		     <FP .P .PP>>>
	<SET PC <AND <OR <SET PP <DRDMSG .A <GETFIX "CONDITIONS">>>
			 <SET PP <DRDMSG .A <GETFIX "PROCESSING-COMPLETED">>>>
		     <FP .P .PP>>>
	<SET PN <AND <SET PP <DRDMSG .A <GETFIX "PROCESSING-NEEDED">>>
		     <FP .P .PP>>>
	<PRC-HISTORY .P .A>
	<COND (<AND .PN <N==? <2 .PN> -1>>
	       <PRINC "  >> ">
	       <DPRT <2 .PN>>
	       <PRINC " -- ">
	       <PRINC .P>
	       <PRINC " scheduled.">
	       <CRLF>)
	      (<AND .PC <OR <NOT .EC> <G? <2 .PC> <2 .EC>>>>
	       <PRINC "     ">
	       <DPRT <2 .PC>>
	       <PRINC " -- ">
	       <PRINC .P>
	       <PRINC " completed.">
	       <CRLF>)
	      (.EC
	       <PRINC "  ** ">
	       <DPRT <2 .EC>>
	       <PRINC " -- ">
	       <PRINC .P>
	       <PRINC " is in Error Hold; ">
	       <CRLF>
	       <PRINC "  ">
	       <PRIN1 <3 .EC>>
	       <CRLF>)
	      (.V? <PRINC "     <>"> <CRLF>)>>

<DEFINE PRC-HISTORY (P A "AUX" DA DE (OUTCHAN .OUTCHAN) (OCD <>))
	#DECL ((P A) STRING (DA DE) <OR FALSE LIST> (OCD) FALSE)
	<COND (<OR <N=? .P "DELIVERY"> <=? .A "">>)
	      (<AND <SET DA <DRDMSG .A <GETFIX "DELIVERY-ATTEMPTS">>>
		    <NOT <EMPTY? .DA>>
		    <SET DE <DRDMSG .A <GETFIX "NET-DELIVERY-ERRORS">>>
		    <NOT <EMPTY? .DE>>>
	       <MAPF <>
		     <FUNCTION (DT CD) 
			  #DECL ((DT) FIX (CD) <FALSE FIX>)
			  <PRINC "     ">
			  <DPRT .DT>
			  <PRINC " -- ">
			  <COND (<=? .CD .OCD> <PRINC "(ditto)">)
				(ELSE <DLV-EXPLAIN .CD>)>
			  <SET OCD .CD>
			  <CRLF>>
		     .DA
		     .DE>)>>

<DEFINE FP (P L)
	#DECL ((L) LIST (P) STRING)
	<MAPF <>
	      <FUNCTION (PL)
		   #DECL ((PL) ANY)
		   <AND <TYPE? .PL LIST>
			<NOT <EMPTY? .PL>>
			<=? <PNAM .PL> .P>
			<MAPLEAVE .PL>>>
	      .L>>

<DEFINE DPRT (DD "AUX" D (OUTCHAN .OUTCHAN)) 
	#DECL ((DD) <OR FALSE FIX LIST UVECTOR>
	       (D) <LIST [2 <LIST [3 FIX]>] STRING>
	       (OUTCHAN) CHANNEL)
	<COND (<NOT .DD> <PRINC "ASAP">)
	      (ELSE
	       <COND (<TYPE? .DD UVECTOR FIX> <SET D <LTIME .DD>>)
		     (ELSE <SET D .DD>)>
	       <PNUM <2 <1 .D>>>
	       <PRINC !\/>
	       <PNUM <3 <1 .D>>>
	       <PRINC !\ >
	       <PNUM <1 <2 .D>>>
	       <PRINC !\:>
	       <PNUM <2 <2 .D>>>)>>

<DEFINE PNUM (N "AUX" (OUTCHAN .OUTCHAN))
	#DECL ((N) FIX (OUTCHAN) CHANNEL)
	<COND (<L? .N 10> <PRINC !\0>)>
	<PRINC .N>>

"DLV-ERR -- delivery error handler"

<DEFINE DLV-EXPLAIN (ERR "AUX" (ECOD <1 .ERR>) (OUTCHAN .OUTCHAN))
	#DECL ((ECOD) FIX (ERR) <FALSE FIX> (OUTCHAN) CHANNEL)
	<COND (<==? .ECOD 1>
	       <PRINC 					
"A user-specified function returned a failure value of:  ">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 2>
	       <PRINC
"The DELIVERY-SPECIFICATIONS field was not in the correct format.">
	       <COND (<LENGTH? .ERR 1>)
		     (ELSE
		      <PRINC
"
The unusable specification was:

">
		      <PRINC <2 .ERR>>)>)
	      (<==? .ECOD 4>
	       <PRINC
"The format to use for outputting the message was ill-formed in the way
described below:

">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 5>
	       <PRINC
"The format to use for ">
	       <PRINC <2 .ERR>>
	       <PRINC " output was ill-formed.
The format was:

">
	       <PRINC <3 .ERR>>)
	      (<==? .ECOD 6>
	       <PRINC
"The format of the ">
	       <PRINC <2 .ERR>>
	       <PRINC " field was ill-formed.  The format was:

">
	       <PRINC <3 .ERR>>)
	      (<==? .ECOD 7>
	       <PRINC
"The MAIL output file could not be opened.  The error code returned was:

">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 8>
	       <PRINC
"The user specified for output did not exist: ">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 9>
	       <PRINC
"The datum specified to contain the output format for ">
	       <PRINC <2 .ERR>>
	       <PRINC
" was not a valid format:

">
	       <PRINC <3 .ERR>>)
	      (<==? .ECOD 10>
	       <PRINC
"The foreign host refused the message.  I.e., the FTP connection was
made, but no 350 reply was received on attempting to transfer the mail.">)
	      (<==? .ECOD 11>
	       <PRINC
"An MDL error occurred during the network transfer procedure.  This
indicates a bug in the mailer demon code, and has been reported.  The
error arguments were:

">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 12>
	       <PRINC
"It was applied to the author area of the message.">)
	      (<==? .ECOD 13>
	       <PRINC "The ICP attempt was unsuccessful.">)
	      (<==? .ECOD 14>
	       <PRINC
"An I/O channel error occurred during the network transfer.  Generally
this occurs when the foreign host or net crashes.  The reason given was:
">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 15>
	       <PRINC 
"An IO-channel error occured as the mail was being output:

">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 16>
	       <PRINC 
"A MDL error occured while the mail was being output.  This indicates
a bug in one of the mail printing routines and has been reported.  The
error was:

">
	       <PRINC <2 .ERR>>)
	      (<==? .ECOD 17>
	       <PRINC
"The transfer was terminated by a (supposedly) transient error.
">
	       <COND (<NOT <LENGTH? .ERR 1>>
		      <PRINC "The FTP error reply was:">
		      <CRLF>
		      <CRLF>
		      <PRINC <2 .ERR>>)
		     (ELSE
		      <PRINC 
"The probable cause is overloading of the recipient site.">)>)
	      (<==? .ECOD 18>
	       <PRINC
"The transfer was not acknowledged by the foreign site.  I.e., the
message was transferred, but no 256 reply was received.">)
	      (<==? .ECOD 19>
	       <PRINC
"The foreign site refused the message.  ">
	       <COND (<NOT <LENGTH? .ERR 1>>
		      <PRINC "The FTP error reply was:

">
		      <PRINC <2 .ERR>>)
		     (ELSE
		      <PRINC
"The probable cause is that
the specified user does not have a mailbox at the site.">)>)
	      (<==? .ECOD 20>
	       <PRINC "The destination host was known to be down.">)
	      (<==? .ECOD 21>
	       <PRINC
"An FTP connection was established but no herald was ever received.
This indicates a problem with the remote site's FTP server.">)
	      (ELSE <PRINC "Unknown error: "> <PRIN1 .ERR>)>>

<ENDPACKAGE>

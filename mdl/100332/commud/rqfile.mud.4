
<PACKAGE "RQFILE">

;"PACKAGE FOR HANDLING REQUEST FILES FOR DAEMONS"

<ENTRY RQNEXT RQTEMP RQLOSE RQEXIST? RQDELE RQRCVR>

<USE "CHAN" "M-DEFS" "M-DATA" "ACHOMP" "OPSYS">

"RQNEXT -- given string, naming request file, returns an open
  ACHANNEL to next file to be hacked.  used to actually process
  requests.  the string should be the same as the one used by
  RQOPEN to create the request files."

<SETG RQBUF <FREEZE <ISTRING 500>>>

<DEFINE RQNEXT ("AUX" (SNM <DATUM "COMSYS-MESSAGE-DIR">)) 
	#DECL ((VALUE) <OR FALSE ACHANNEL> (SNM) <SPECIAL STRING>)
	<OR <ACOPEN <DATUM "COMSYS-MOBY-FILE"> ,RQBUF>
	    <ACOPEN <DATUM "COMSYS-INPUT-FILE"> ,RQBUF>>>

"RQTEMP -- used by a process which has finished processing a request
  file obtained from RQNEXT.  this function renames the file to indicate
  that it has been processed.  the procedure using these functions should
  call RQDELE on the 'renamed' requests to get rid of them when their
  effects have been permanently recorded.  the function RQRCVR is used
  when starting to retrieve any 'done' requests which were not deleted,
  presumably because of a crash, etc.
  the supplied string should be used to specify the first name to be used
  to save the processed file."

<DEFINE RQTEMP (QCH "AUX" (CH <ACCHAN .QCH>) R) 
	#DECL ((ST) STRING (QCH) ACHANNEL (CH) CHANNEL (R) <OR ATOM FALSE>)
	<CLOSE .CH>
	<RENAME <FILCHN .CH>
		TO
		<OPSYS
		  (ITS <DATUM "COMSYS-INPUT-TEMP">)
		  (TOPS-20 
		   <STRING !\< <RDIRECTORY .CH> !\> <DATUM "COMSYS-INPUT-TEMP">>)>>>

<DEFINE RQLOSE (QCH "AUX" (CH <ACCHAN .QCH>) R) 
	#DECL ((ST) STRING (QCH) ACHANNEL (CH) CHANNEL (R) <OR ATOM FALSE>)
	<CLOSE .CH>
	<COND (<SET R
		    <RENAME <FILCHN .CH>
			    TO
			    <OPSYS
			     (ITS <DATUM "COMSYS-INPUT-TEMP">)
			     (TOPS-20 
			      <STRING !\< <RDIRECTORY .CH> !\>
				      <DATUM "COMSYS-LOSER-FILE">>)>>>)
	      (<ERROR RENAME-FAILED RQLOSE .R>)>>

"RQEXIST? -- given file name, returns T if there are requests"

<DEFINE RQEXIST? ("AUX" (SNM <DATUM "COMSYS-MESSAGE-DIR">)) 
	#DECL ((VALUE) <OR ATOM FALSE> (SNM) <SPECIAL STRING>)
	<OPSYS (ITS
		<OR <FILE-EXISTS? <DATUM "COMSAT-INPUT-FILE">>
		    <FILE-EXISTS? <DATUM "COMSYS-INPUT-FILE">>
		    <FILE-EXISTS? <DATUM "COMSYS-MOBY-FILE">>>)
	       (TOPS-20
		<OR <FILE-EXISTS? <DATUM "COMSYS-INPUT-FILE">>
		    <FILE-EXISTS? <DATUM "COMSYS-MOBY-FILE">>>)>>

"RQDELE -- given name of request file (usually 'old' name file)
  deletes all such files.  used generally to delete processed files, when
  certain that their effects have been permanently recorded.
 -- returns # of files deleted"

<DEFINE RQDELE ("AUX" (ST <DATUM "COMSYS-INPUT-TEMP">) (I 0)
		      (SNM <DATUM "COMSYS-MESSAGE-DIR">)) 
	#DECL ((I VALUE) FIX (ST) STRING (SNM) <SPECIAL STRING>)
	<REPEAT ()
		<OR <RENAME .ST> <RETURN .I>>
		<SET I <+ .I 1>>>>

"RQRCVR -- given name of request file and 'old' request files, renames
  any existing old files to be requests again.  used for crash recovery.
  returns the number of files recovered.
	the optional name2 argument is used to specify whether the files
  should be renamed to be '>', or to retain the same second name.  the
  latter is the default.
 	if files retain the same name 2 -- the optional arg upd, (default false)
  controls what happens if the file with that name exists already -- if
   upd is false, the 'old' file will simply be deleted, else error."

<DEFINE RQRCVR ("AUX" (SNM <DATUM "COMSYS-MESSAGE-DIR">)
		      (STNEW <DATUM "COMSYS-NEW-FILE">)
		      (STOLD <DATUM "COMSYS-INPUT-TEMP">)
		      (I 0)) 
   #DECL ((STOLD STNEW) STRING (I VALUE) FIX (SNM) <SPECIAL STRING>)
   <OPSYS (ITS <SET STOLD <STRING .STOLD " <">>)>
   <REPEAT ()
	   <OR <RENAME .STOLD TO .STNEW> <RETURN .I>>
	   <SET I <+ .I 1>>>>

<ENDPACKAGE>

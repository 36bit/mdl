	TITLE RUNCMP

.DECSAV

O=0
A=1
B=2
C=3
D=4
E=5
F=6

P=17

LOC 140

PCLEV1:	0
LEVTAB:	0,,PCLEV1
	0
	0
CHNTAB:	BLOCK	19.
	1,,INFINT
	BLOCK	16.
INFHDL:	0
STRTIM:	0
PDL:	BLOCK	10

START:	RESET
	MOVE	P,[-10,,PDL]
	MOVEI	A,.FHSLF
	MOVE	B,[LEVTAB,,CHNTAB]
	SIR
	EIR
	MOVE	B,[<1_<35.-.ICIFT>>]
	AIC
	MOVE	A,[GJ%SHT\GJ%OLD]
	HRROI	B,[ASCIZ /SYS:NPCOMP.EXE/]
	GTJFN
	 JRST	STRTER
	PUSH	P,A
	HRLZI	A,(CR%CAP)
	CFORK
	 JRST	STRTER
	MOVEM	A,INFHDL
	GTAD
	MOVEM	A,STRTIM
	MOVE	A,INFHDL
	HRLS	A
	HRR	A,(P)
	GET
	MOVE	A,INFHDL
	MOVEI	B,0
	SFRKV
	HRROI	A,[ASCIZ /Compiler started.  Load is /]
	PSOUT
	MOVE	A,[SIXBIT /SYSTAT/]
	SYSGT
	HRRZ	A,B
	HRLI	A,14
	GETAB
	 JRST	CRLF
	MOVE	B,A
	MOVEI	A,.PRIOU
	MOVE	C,[024000,,020200]
	FLOUT
	 JFCL
CRLF:	HRROI	A,[ASCIZ /.
/]
	PSOUT
WLOOP:	MOVE	A,[300.*1000.]
	DISMS
	MOVE	A,INFHDL
	RUNTM			; READ RUNTIME
	IDIV	A,B		; CONVERT TO SECONDS
	CAIG	A,7200.		; PAST TWO HOURS?
	 JRST	WLOOP		; NO
	MOVEI	A,.FHSLF
	DIR
	MOVE	A,INFHDL
	HFORK
	CLZFF
	PUSHJ	P,PRTTIM
	HRROI	A,[ASCIZ /Compiler ran overtime.
/]
	ESOUT
	HALTF
	JRST	START
STRTER:	HRROI	A,[ASCIZ /Couldn't load compiler.
/]
	ESOUT
	HALTF
	JRST	START

INFINT:	MOVE	A,INFHDL
	CLZFF
	HRROI	A,[ASCIZ /Compiler halted.  /]
	PSOUT
	PUSHJ	P,PRTTIM
	HALTF
	JRST	START

PRTTIM:	HRROI	A,[ASCIZ /Used /]
	PSOUT
	MOVE	A,INFHDL
	RUNTM
	MOVE	D,A		; RUNTIME IN MILLISECONDS TO D
	MOVEI	A,.PRIOU
	MOVEI	F,0
	IDIV	D,[3600.*1000.]	; # HOURS IN D
	JUMPE	D,PRTMIN
	MOVNI	F,1
	MOVE	B,D
	MOVE	C,10.
	NOUT
	 JFCL
	MOVEI	B,":
	BOUT
PRTMIN:	MOVE	D,E
	IDIVI	D,60000.
	JUMPE	D,[SKIPN F
		    JRST PRTSEC
		   HRROI A,[ASCIZ /00:/]
		   PSOUT
		   MOVEI A,.PRIOU
		   JRST PRTSEC]
	MOVE	B,D
	MOVEI	C,10.
	SKIPE	F
	 MOVE	C,[(NO%LFL\NO%ZRO)+2,,10.]
	NOUT
	 JFCL
	MOVNI	F,1
PRTSEC:	MOVE	D,E
	IDIVI	D,1000.
	MOVE	B,D
	MOVEI	C,10.
	SKIPE	F
	 MOVE	C,[(NO%LFL\NO%ZRO)+2,,10.]
	NOUT
	 JFCL
	MOVEI	B,".
	BOUT
	MOVE	B,E
	IDIVI	B,100.
	MOVEI	C,10.
	NOUT
	 JFCL
	HRROI	A,[ASCIZ / in /]
	PSOUT
	GTAD
	SUB	A,STRTIM
	IMULI	A,86400.
	IDIV	A,[1000000]
	MOVE	D,A
	MOVEI	B,0
	MOVEI	C,0
	MOVEI	A,.PRIOU
	MOVSI	E,(OT%NDA\OT%SCL)
	ODTNC
	HRROI	A,[ASCIZ /.
/]
	PSOUT
	POPJ	P,

END	START
	
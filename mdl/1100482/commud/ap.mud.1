
<PACKAGE "AP">

<ENTRY AP?>

<GDECL (EH) HANDLER>

<USE "M-DEFS">

<DEFINE AP? (OB "AUX" ANS (EN? <>)) 
   #DECL ((OB) ANY (VALUE ANS) <OR FALSE APPLICABLE> (EN?) <OR ATOM FALSE>)
   <SET ANS
    <PROG ERRLOC ()
	  #DECL ((ERRLOC) <SPECIAL ACTIVATION>)
	  <COND (<APPLICABLE? .OB> <RETURN .OB>)>
	  <COND (<NOT .EN?> <WATCH EH "ERROR" ,APHAND 4> <SET EN? T>)>
	  <COND (<TYPE? .OB FORM>			   ;"SOMETHING TO EVAL?"
		 <SET OB <EVAL .OB>>
		 <AGAIN>)>
	  <COND (<TYPE? .OB STRING> <SET OB <PARSE .OB>> ;"GET ATOM OR WHATEVER"
		 <AGAIN>)>
	  <COND (<TYPE? .OB ATOM>
		 <COND (<AND <GASSIGNED? .OB> <APPLICABLE? ,.OB>> ,.OB)
		       (ELSE #FALSE ("CANNOT FIND ATOM VALUE"))>)
		(ELSE #FALSE ("UNUSABLE OBJECT DESCRIPTION"))>>>
   <COND (.EN? <OFF ,EH>)>
   .ANS>

<DEFINE APHAND ("TUPLE" STUFF) 
	#DECL ((STUFF) TUPLE (ERRLOC) ACTIVATION)
	<AND <ASSIGNED? ERRLOC>
	     <LEGAL? .ERRLOC>
	     <DISMISS #FALSE ("ERROR IN AP?") .ERRLOC>>>

<ENDPACKAGE>

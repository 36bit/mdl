TITLE GCLOAD ROUTINE TO LOAD GARBAGE COLLECTOR

; AC DEFS

A=1
B=2
C=3
D=4
E=5
F=6
P=17

; INFO FOR CORBLKS

CRJB==1000,,400001
WRTP==1000,,100000
CWTP==1000,,4000
IMAPCH==0
MAPCHN==1000,,IMAPCH
FME==1000,,-1


; DEFINITIONS OF BOUNDS

LENGC==1				; LENGTH OF GARBAGE COLLECTOR FILE
STARTGC==350				; START OF GARBAGE COLLECTOR FILE

; AOBJN POINTERS USED

GCPTTR== -LENGC,,STARTGC		; POINTER TO GC IN CORE IMAGE
FPTR==-LENGC,,0				; POINTER TO GC IN FILE
IOPTR== -LENGC*2000,,STARTGC*2000

LOAD:	MOVE	P,[-400,,PDL]
	.SUSET	[.SSNAM,,GCDIR]		; SET SNAME TO GARBAGE COLLECTOR
	.OPEN	IMAPCH,GCBLK		; OPEN FILE TO GC
	PUSHJ	P,SLEEP
	MOVE	A,[GCPTTR]		; POINTER IN CORE IMAGE
	MOVE	B,[FPTR]		; POINTER IN FILE		
	.CALL	[SETZ
		 SIXBIT /CORBLK/
		 CWTP
		 FME
		 A
		 MAPCHN
	         SETZ B]
	.VALUE	[ASCIZ /: FATAL CORBLK FAILED /]
	.CLOSE	IMAPCH,
	.VALUE	LODSYM
	.VALUE	0
	JRST	.-1

DUMP:	.SUSET	[.SSNAM,,GCDIR]
	.OPEN	IMAPCH,WGCBLK
	PUSHJ	P,SLEEP
	MOVE	A,[IOPTR]
	.IOT	IMAPCH,A
	.CLOSE	IMAPCH,
	.VALUE	[ASCIZ /:KILL	/]


SLEEP:	MOVEI	0,1
	.SLEEP
	SOS	(P)
	SOS	(P)
	POPJ	P,


PDL:	BLOCK	400
GCBLK:	SIXBIT /  &DSK/
	SIXBIT /SGC/
	SIXBIT /MUD/

WGCBLK:	SIXBIT /  'DSK/
	SIXBIT /NSGC/
	SIXBIT /MUD/

GCDIR:	SIXBIT /MUDSAV/
LODSYM:	ASCIZ /:SYMADD MUDSAV;TS MUD
/

END LOAD
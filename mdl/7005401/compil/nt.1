
	<NEWTYPE OPCODE!-OP WORD>


	<TITLE OPCS>
	<DECLARE ("VALUE" OPCODE!-OP CHARACTER)>
	<PUSH TP* (AB)>
	<PUSH TP* 1(AB)>
	<PUSHJ P* OPCS1>
	<JRST FINIS>

	<INTERNAL-ENTRY OPCS1 1>
	<SUBM M* (P)>
	<SUB TP* [<2 (2)>]>			; "CLEAN OFF USELESS ARGUMENT"
	<PUSH P* [0]>				; "NUMBER OF ARGUMENTS TO STRING"
	<MOVSI A* <TYPE-CODE ATOM>>		; "GET CHANNEL AND PUSH IT"
	<MOVE B* <MQUOTE INCHAN>>
	<PUSHJ P* CILVAL>
	<PUSH TP* A>
	<PUSH TP* B>
	<PUSH P* [0]>
LP1	<MOVE A* -1(TP)>
	<MOVE B* (TP)>
	<PUSHJ P* CNXTC1>
	<CAIN B* *50*>			; "SEE IF ("
	<SETOM	-1(P)>
	<CAIE B* *51*>			; "SEE IF )"
	<JRST	LP3>
	<SKIPN	-1(P)>			; "DONT SKIP IF JUST DELIMETER"
	<JRST LP2>
	<SETZM -1(P)>
LP3	<CAIG	B* *132*>
	<CAIGE B* *50*>
	<JRST LP2>
	<CAILE	B* *71*>
	<CAIL	B* *100*>
	<SKIPA B* (TP)>			; "GET BACK CHANNEL"
	<JRST	LP2>
	<PUSHJ P* CREDC1>
	<MOVE A* (P)>
	<LSH A* 6>
	<SUBI B* *40*>
	<DPB B* [<(*000600* ) A>]>
	<MOVEM A* (P)>
	<JRST LP1>
LP2	<PUSH TP* <TYPE-WORD FIX>>
	<POP P* A>
	<PUSH TP* A>
	<MOVE A* <MQUOTE <RGLOC OPT T>>>
	<ADD A* GLOTOP 1 (TVP)>
	<PUSH TP* <TYPE-WORD UVECTOR>>
	<PUSH TP* 1(A)>
	<PUSHJ P* BIN1>
	<JRST ERR>
	<MOVSI A* <TYPE-CODE OPCODE!-OP >>
	<SUB P* [<1 (1)>]>
	<JRST MPOPJ>
ERR	<PUSH TP* <MQUOTE "OPCODE NON-EXISTANT"> -1>
	<PUSH TP* <MQUOTE "OPCODE NON-EXISTANT">>
	<JRST CALER1>



	
BIN1	<SUBM M* (P)>
CONT1	<HLRE	B* (TP)>
	<MOVNS	B>
	<MOVE	A* (TP)>
	<HRLI	B* (B)>		; "B IS CURRENT OFFSET"
	<MOVE	E* -2(TP)>

UP	<ASH	B* -1>		; "HALVE TABLE"
	<AND	B* [<-2 (-2)>]>	; "FORCE DIVIS BY 2"
	<MOVE	C* A>		; "COPY POINTER"
	<JUMPLE	B* LSTHLV>	; "CANT GET SMALLER"
	<ADD	C* B>
	<CAMLE	E*  (C)>		; "SKIP IF EITHER FOUND OR IN TOP"
	<MOVE	A* C>		; "POINT TO SECOND HALF"
	<CAMN	E* (C)>		; "SKIP IF NOT FOUND"
	<JRST	WON>
	<CAML	E* (C)>		; "SKIP IF IN TOP HALF"
	<JRST	UP>
	<HLLZS	C>		; "FIX UP OINTER"
	<SUB	A* C>
	<JRST	UP>

WON	<MOVE	B* 1(C)>		; "RET VAL IN E"
	<SOS (P)>
	<MOVE	A* <TYPE-WORD FIX>>
LOSS	<SUB	TP* [<4 (4)>]>
	<JRST	MPOPJ>

LSTHLV	<CAMN	E* (C)>		; "LINEAR SERCH REST"
	<JRST	WON>
	<ADD	C* [<2 (2)>]>
	<JUMPL	C* HERE -3>
	<JRST LOSS>
	



	<SUB-ENTRY OUTPUT-OPCODE ("VALUE" ANY OPCODE!-OP )>
	<PUSH TP* (AB)>
	<PUSH TP* 1 (AB)>
	<PUSHJ P* IOUT>
	<JRST FINIS>

	<INTERNAL-ENTRY IOUT 1>
	<SUBM M* (P)>
	<MOVE B* (TP)>
	<PUSH TP* <TYPE-WORD FIX>>
	<PUSH TP* B>
	<PUSH TP* <TYPE-WORD UVECTOR>>
	<MOVE A* <MQUOTE <RGLOC OOPT T>>>
	<ADD A* GLOTOP 1 (TVP)>
	<PUSH TP* 1(A)>
	<PUSHJ P* BIN1>
	<JRST PRINTN>
	<PUSH P* [6]>
	<PUSH P* B>
	<MOVSI A* <TYPE-CODE ATOM>>
	<MOVE B* <MQUOTE OUTCHAN>>
	<PUSHJ P* CILVAL>
	<PUSH TP* A>
	<PUSH TP* B>
	<GETYP E* A>
	<CAIE E* <TYPE-CODE CHANNEL>>
	<JRST STARTP>
	<MOVE E* 27 (B)>
	<ADDI E* 7>
	<CAMGE E* 25 (B)>
	<JRST STARTP>
	<MOVEI D* 13>		; "Carriage ret lf first"
	<PUSHJ	P* CPCH>
	<MOVEI	D* 10>
	<MOVSI	A* <TYPE-CODE CHANNEL>>
	<MOVE	B* (TP)>
	<PUSHJ	P* CPCH>
	<MOVSI	A* <TYPE-CODE CHANNEL>>
	<MOVE	B* (TP)>

STARTP	<MOVEI D* <ASCII !"`>>
	<PUSHJ P* CPCH>
LPS	<MOVE A* (P)>
	<LDB B* [<(*360600*) A>]>
	<LSH A* 6>
	<MOVEM A* (P)>
	<JUMPE B* LPF>
	<MOVE D* B>
	<MOVE A* -1(TP)>
	<MOVE B* (TP)>
	<ADDI D* *40*>
	<PUSHJ P* CPCH>
LPF	<SOSLE -1(P)>
	<JRST LPS>
	<POP TP* B>
	<POP TP* A>
	<MOVEI D* <ASCII !" >>
	<PUSHJ P* CPCH>
	<SUB P* [<2 (2)>]>
LEAVE	<SUB TP* [<2 (2)>]>
	<JRST MPOPJ>

PRINTN	<MOVSI A* TATOM>
	<MOVE B* <MQUOTE OUTCHAN>>
	<PUSHJ P* CILVAL>
	<PUSH TP* A>
	<PUSH TP* B>
	<MOVEI D* <ASCII !"#>>
	<PUSHJ P* CPCH>
	<MOVE A* -1(TP)>
	<MOVE B* (TP)>
	<MOVE D* <MQUOTE OPCODE!-OP>>
	<PUSHJ P* CP1ATM>
	<MOVE A* -1(TP)>
	<MOVE B* (TP)>
	<MOVEI D* <ASCII !" >>
	<PUSHJ P* CPCH>
	<POP TP* B>
	<POP TP* A>
	<MOVE C* <TYPE-WORD FIX>>
	<MOVE D* (TP)>
	<PUSHJ P* CIPRN1>
	<JRST LEAVE>
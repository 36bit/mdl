TITLE SUBSYS -- Tops-20 Muddle Subsystem Bootstrapper

	.DECSAV

; ask for save file name and mdl version
IF1,[	PRINTC /Type in SAVE file name: /
	.TTYMAC A
	DEFINE SAVNAM
	 ASCIZ /A/
	TERMIN
	TERMIN

]

	A=1
	B=2
	C=3
	D=4
	E=5

	LOC	140

START:	HRLZI	A,100001
	MOVE	B,[440700,,SAVFIL]
	GTJFN
	 JRST	NOSAVE
	MOVEI	0,(A)			;JFN TO SAVE FILE
	TLZ	A,-1
	MOVE	B,[440000,,240000]
	OPENF				; HAS TO BE OPEN
	 JRST	NOSAV1
	BIN
	MOVE	D,B
; set access back to beginning
	MOVEI	B,0
	SFPTR
	 HALTF
; create muddle version number
	MOVE	B,[440700,,FILE]
	MOVE	C,[440700,,D]
	ILDB	E,B
	CAIE	E,"X	; "X" SIGNALS START OF VERSION NUMBER
	 JRST	.-2
VERLUP:	ILDB	E,C
	CAIN	E,40	; SPACE SIGNALS END OF VERSION
	 JRST	RDMDL
	DPB	E,B
	IBP	B
	JRST	VERLUP

; now try to read it
RDMDL:	HRLZI	A,100001
	MOVE	B,[440700,,FILE]
	GTJFN				; JFN TO INTERPRETER
	 JRST	NOMDL
	HRLI	A,400000
	MOVE	BLTPTR,[LOADGO,,B]
	BLT	BLTPTR,BLTPTR
	JRST	B

LOADGO:	GET				; LOAD INTERPRETER
	MOVEI	A,400000
	GEVEC				; CONS STARTING ADDRESS
	JRST	1(B)			; JRST TO START+1 IN INTERPRETER

BLTPTR=.-LOADGO+1


; junk past here is only used if there are errors
NOSAVE:	MOVE	B,A
	HRROI	A,[ASCIZ /Can't find SAVE file? (/]
	PSOUT
	HRROI	A,SAVFIL
NOFILE:	PSOUT
	HRROI	A,[ASCIZ /): /]
	PSOUT
	JRST	ERPRNT

NOSAV1:	MOVE	B,A
       	HRROI	A,[ASCIZ /Can't OPENF SAVE file? (/]
	PSOUT
	HRROI	A,SAVFIL
	JRST	NOFILE

NOMDL:	MOVE	B,A
	HRROI	A,[ASCIZ /No Muddle Interpreter? (/]
	PSOUT
	HRROI	A,FILE
	JRST	NOFILE

ERPRNT:	HRRZI	A,-1
	HRLI	B,400000
	MOVEI	C,0
	ERSTR		; PRINT ERROR
	 HALTF	;UNDEFINED ERROR.
	 HALTF	;CHOMPING DEST.
	 HALTF	;WON.

FILE:	ASCIZ	/PS:<MDL>MDLXXX.EXE/
SAVFIL:	SAVNAM

	END	START

;"(c) Copyright 1978 Massachusetts Institute of Technology.  All Rights Reserved."

%%<USE "JSYS">

<TITLE PRINTA>
	<DECLARE ("VALUE" ANY CHANNEL <OR UVECTOR <PRIMTYPE WORD>> FIX)>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* PMPRINT>
	<JRST	FINIS>

<INTERNAL-ENTRY PMPRINT 3>
	<SUBM	M* (P)>
	<MOVE	D* [<SOUT>]>
	<JRST	INA>

<SUB-ENTRY READA ("VALUE" ANY CHANNEL <OR UVECTOR <PRIMTYPE WORD>> FIX)>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* PMREAD>
	<JRST	FINIS>

<INTERNAL-ENTRY PMREAD 3>
	<SUBM	M* (P)>
	<MOVE	D* [<SIN-JSYS>]>
INA	<MOVE	A* <MQUOTE '%<RGLOC TENEX T>>>
	<ADD	A* GLOTOP 1 (TVP)>
	<GETYP	A* (A)>
	<CAIN	A* <TYPE-CODE ATOM>>
	<JRST	TENEX>
	<HRLZI	A* (*IOT)>
	<MOVE	B* -4(TP)>
	<HRRZ	B* 1(B)>	; "CHANNEL NUMBER"
	<DPB	B* [<(*270400*) 1>]>
	<HRLZ	O* (TP)>
	<MOVNS	O>
	<HRR	O* -2(TP)>
	<XCT	A>
TENEND	<MOVE	A* -5(TP)>
	<MOVE	B* -4(TP)>
	<SUB	TP* [<(6) 6>]>
	<JRST	MPOPJ>
	
TENEX	<MOVE	A* -4(TP)>
	<MOVE	A* 1(A)>
	<MOVE	B* -2(TP)>
	<MOVN	C* (TP)>
	<HRLI	B* *444400*>
	<XCT	D>
	<JRST	TENEND>

<TITLE	PAGE-CLEAR>
	<DECLARE ("VALUE" 'T FIX FIX)>
	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* CPAGE>
	<JRST	FINIS>

<INTERNAL-ENTRY CPAGE 2>
	<SUBM	M* (P)>
	<MOVE	A* -2(TP)>
	<IMULI	A* 1024>
	<SETZM	(A)>
	<HRL	A* A>
	<AOJ	A*>
	<HLRZ	B* A>
	<MOVE	C* (TP)>
	<IMULI	C* 1024>
	<SUBI	C* 1>
	<ADD	B* C>
	<BLT	A* (B)>	
	<MOVE	A* <TYPE-WORD ATOM>>
	<MOVE	B* <MQUOTE T>>
	<SUB	TP* [<4 (4)>]>
	<JRST	MPOPJ>	

<TITLE	CLEAR-UV>
	<DECLARE ("VALUE" UVECTOR UVECTOR)>
 	<DPUSH	TP* (AB)>
	<PUSHJ	P* CUV>
	<JRST	FINIS>

<INTERNAL-ENTRY CUV 1>
	<SUBM	M* (P)>	
	<MOVE	A* (TP)>
	<SETZM	(A)>
	<HLRZ	B* A>
	<MOVNS	B>
	<SUBI	B* 1>
	<ADD	B* A>
	<HRL	A* A>
	<ADDI	A* 1>
	<BLT	A* (B)>
	<MOVE	A* -1(TP)>
	<MOVE	B* (TP)>
	<SUB	TP* [<2 (2)>]>
	<JRST	MPOPJ>

<TITLE CLEAR-VEC>
	<DECLARE ("VALUE" VECTOR VECTOR)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* CLVEC>
	<JRST	FINIS>

<INTERNAL-ENTRY CLVEC 1>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<JUMPGE	A* CLDONE>
CLLOOP	<SETZM	1(A)>
	<ADD	A* [<2 (2)>]>
	<JUMPL	A* CLLOOP>
CLDONE	<MOVE	A* -1(TP)>
	<MOVE	B* (TP)>
	<SUB	TP* [<2 (2)>]>
	<JRST	MPOPJ>

<TITLE AOBJECT>
	<DECLARE ("VALUE" UVECTOR ANY UVECTOR)>
	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<PUSHJ	P* ALOC1>
	<JRST	FINIS>

<INTERNAL-ENTRY ALOC1 2>
	<SUBM	M* (P)>
	<MOVE	B* (TP)>
	<MOVE	A* -3(TP)>
	<MOVEM	A* (B)>
	<MOVE	A* -2(TP)>
	<MOVEM	A* 1(B)>
	<MOVE	A* -1(TP)>
	<SUB	TP* [<4 (4)>]>
	<JRST	MPOPJ>

<TITLE GET-VECTOR>
	<DECLARE ("VALUE" <OR FALSE VECTOR> <PRIMTYPE WORD>)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* GETVEC1>
	<JRST	FINIS>

<INTERNAL-ENTRY GETVEC1 1>
	<SUBM	M* (P)>
	<MOVE	A* <TYPE-WORD VECTOR>>
	<SKIPN	B* @ (TP)>
	<MOVE	A* <TYPE-WORD FALSE>>
	<SUB	TP* [<2 (2)>]>
	<JRST	MPOPJ>

<TITLE MOVE-M>
	<DECLARE ("VALUE" 'T <PRIMTYPE WORD> <PRIMTYPE WORD>)>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<PUSHJ	P* MOVEM1>
	<JRST	FINIS>

<INTERNAL-ENTRY MOVEM1 2>
	<SUBM	M* (P)>
	<MOVE	A* -2(TP)>
	<MOVEM	A* @ (TP)>
	<MOVE	A* <TYPE-WORD ATOM>>
	<MOVE	B* <MQUOTE T>>
	<SUB	TP* [<4 (4)>]>
	<JRST	MPOPJ>

<TITLE GETLOC>
	<DECLARE ("VALUE" WORD <PRIMTYPE WORD>)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* GETLOC1>
	<JRST	FINIS>

<INTERNAL-ENTRY GETLOC1 1>
	<SUBM	M* (P)>
	<MOVE	A* <TYPE-WORD WORD>>
	<MOVE	B* @ (TP)>
	<SUB	TP* [<2 (2)>]>
	<JRST	MPOPJ>
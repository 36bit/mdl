;"(c) Copyright 1978 Massachusetts Institute of Technology.  All Rights Reserved."


; "AFIND takes a page-number and a number of pages and returns
   a PBLOCK with that number of pages hopefully including the
   one requested as high page"


<TITLE APGFIND>
	<DECLARE ("VALUE" <OR FALSE FIX> FIX FIX <UVECTOR [REST WORD]>)>
	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<DPUSH	TP* 4(AB)>
	<PUSHJ	P* AFIND1>
	<JRST	FINIS>

<INTERNAL-ENTRY AFIND1 3>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<MOVE	B* -4(TP)>	; "PAGE REQUESTED"
	<SKIPGE	B>
	<JRST	SETTL1>
	<CAILE	B* 256>
	<JRST	ILLPAG>
	<IDIVI	B* 36>
	<ADD	A* B>
	<MOVE	D* (A)>
	<MOVE	E* -1(A)>
	<MOVE	O* -2(TP)>	; "NUMBER OF PAGES"
	<CAIL	O* 36>
	<JRST	ILLPAG>
	<ROTC	D* 1(C)>
	<PUSH	P* E>
	<PUSH	P* A>
	<MOVN	A* O>
	<MOVEI	B* 36(A)>	; "LEFT ON THE LEFT"
	<LSH	E* (B)>
	<MOVNS	B>
	<ASH	E* (B)>
	<CAME	E* [-1]>
	<JRST	HUMBUG>
	<MOVNS	A>
	<LSH	E* (A)>		; "GET RID OF THOSE BITS"
	<AND	E* -1(P)>	; "TURN OFF EVERYTHING"
	<MOVNS	C>
	<ROTC	D* -1(C)>	; "PUT EVERYTHING BACK INTO PLACE"
	<POP	P* A>
	<MOVEM	D* (A)>
	<MOVEM	E* -1(A)>
	<SUB	P* [<1 (1)>]>
	<MOVE	A* -5(TP)>
	<MOVE	B* -4(TP)>
	<JRST	AFOUT>

SETTLE	<SUB	P* [<2 (2)>]>
SETTL1	<HRRZ	A* (TP)>
	<MOVE	E* A>
	<ADDI	E* 7>		; "MAXIMUM ADDRESS"
SETTL2	<CAIGE	E* (A)>
	<JRST	LOST>
	<SKIPN	(A)>
	<AOJA	A* SETTL2>	; "FIND FIRST NON-ZERO WORD"
	<HRLI	A* *440100*>	; "MAKE BP TO FIRST BIT"
SETTL3	<MOVE	B* -2(TP)>
SETLP	<CAIG	E* (A)>
	<JRST	LOST>
	<ILDB	C* A>
	<JUMPG	C* FOUND1>
	<JRST	SETLP>

FOUND1	<MOVE	D* A>
	<SUBI	B* 1>
	<JUMPE	B* WON>
	<ILDB	C* A>
	<JUMPE	C* SETTL3>
	<SOJG	B* HERE -2>
WON	<MOVE	B* -2(TP)>
	<ADD	D* [<(*010000*)>]>
	<MOVE	C* D>
	<IOR	C* [<(*327777*) *777777*>]>
	<AOJN	C* WON1>
	<SOS	D>
	<HRLI	D* *010100*>
WON1	<SETZ	C*>
	<IDPB	C* D>			; "LOOP ZEROING ENTRIES"
	<SOJG	B* HERE -1>
	<LDB	C* [<(*360600*) D>]>
	<MOVEI	A* 36>
	<SUB	A* C>
	<HRRZ	B* (TP)>
	<SUBI	B* (D)>
	<CAIN	C* *44*>
	<ADDI	B* 1>
	<MOVNS	B>
	<IMULI	B* 36>
	<ADDI	B* -1(A)>
	<MOVE	C* -2(TP)>
	<SUBI	B* -1(C)>
	<MOVE	A* <TYPE-WORD FIX>>
	<JRST	AFOUT>

ILLPAG	<MOVE	A* <PQUOTE #FALSE ("ILLEGAL-ARGUMENT")>>
	<MOVE	B* <MQUOTE #FALSE ("ILLEGAL-ARGUMENT")>>
	<JRST	AFOUT>

HUMBUG	<SUB	P* [<2 (2)>]>
LOST	<MOVE	A* <PQUOTE #FALSE ("CANT-FILL-REQUEST")>>
	<MOVE	B* <MQUOTE #FALSE ("CANT-FILL-REQUEST")>>
AFOUT	<SUB	TP* [<6 (6)>]>
	<JRST	MPOPJ>

<TITLE APGGIVE>
	<DECLARE ("VALUE" FIX FIX FIX <UVECTOR [REST WORD]>)>
	<DPUSH	TP* (AB)>
	<DPUSH	TP* 2(AB)>
	<DPUSH	TP* 4(AB)>
	<PUSHJ	P* AGIVE1>
	<JRST	FINIS>

<INTERNAL-ENTRY AGIVE1 3>
	<SUBM	M* (P)>
	<MOVE	A* -4(TP)>
	<SUB	A* -2(TP)>
	<ADDI	A* 1>		; "LOW PAGE TO FREE"
	<MOVE	C* (TP)>
	<IDIVI	A* 36>
	<ADD	C* A>		; "POINTS AT CORRECT WORD"
	<MOVNS	B>
	<MOVEI	B* 36(B)>
	<LSH	B* 30>
	<IOR	B* [<(*0100*)>]>
	<HRR	B* C>		; "BP TO FIRST ENTRY"
	<MOVEI	A* 1>
	<MOVE	C* -2(TP)>
	<IDPB	A* B>
	<SOJG	C* HERE -1>
	<MOVE	A* -5(TP)>
	<MOVE	B* -4(TP)>
	<SUB	TP* [<6 (6)>]>	; "RESET TP"
	<JRST	MPOPJ>

<TITLE PAGE-GIVE-TABLE>
	<DECLARE ("VALUE" <OR ATOM FALSE> <UVECTOR [8 WORD]>)>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* IPGT>
	<JRST	FINIS>

<INTERNAL-ENTRY IPGT 1>
	<SUBM	M* (P)>
	<SETZ	B*>
	<MOVEI	D* 8>
	<SKIPA	E* (TP)>
TOPPER	<ADDI	E* 1>
	<MOVEI	C* 36>
	<MOVE	A* (E)>
LOOP	<TLNN	A* *400000*>
	<JRST	LNEXT>
	<PUSH	P* A>
	<PUSH	P* B>
	<PUSH	P* C>
	<PUSH	P* D>
	<PUSH	P* E>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* B>
	<MCALL	1 MADMAN-PAGE-GIVE>
	<POP	P* E>
	<POP	P* D>
	<POP	P* C>
	<POP	P* B>
	<POP	P* A>
LNEXT	<ADDI	B* 1>
	<LSH	A* 1>
	<SOJE	C* LOOP>
	<SETZM	(E)>
	<SOJG	D* TOPPER>
	<MOVE	A* <TYPE-WORD ATOM>>
	<MOVE	B* <MQUOTE T>>
	<SUB	TP* [<2 (2)>]>
	<JRST	MPOPJ>


;"RSUBR TO FIND FREE CORE MADMAN-PAGES"

<TITLE	MADMAN-PAGE-FIND>
	<DECLARE ("VALUE" <OR FIX FALSE> "OPTIONAL" FIX)>
	<JUMPGE	AB* ONEARG>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* IMADMAN-PAGE-FIND2>
	<JRST	FINIS>

ONEARG	<PUSHJ	P* IMADMAN-PAGE-FIND1>
	<JRST	FINIS>

<INTERNAL-ENTRY IMADMAN-PAGE-FIND1 0>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [<1>]>
	<JRST	PF1>

<INTERNAL-ENTRY IMADMAN-PAGE-FIND2 1>
	<SUBM	M* (P)>
PF1	<MOVE	A* (TP)>	;"YES, GET IT"
	<JUMPLE	A* WRONGT>
	<PUSHJ	P* PGFIND>
	<JUMPL	B* [<MOVSI A* TFALSE>
		    <SETZ B*>
		    <JRST PFX>]>
	<MOVSI	A* <TYPE-CODE FIX>>
PFX	<SUB	TP* [<2 (2)>]>
	<JRST	MPOPJ>



;"RSUBR TO GIVE BACK CORE MADMAN-PAGES"

<TITLE	MADMAN-PAGE-GIVE>
	<DECLARE ("VALUE" FIX FIX "OPTIONAL" FIX)>
	<DPUSH	TP* (AB)>
	<AOBJN	AB* HERE 1>
	<AOBJP	AB* ONEARG>
	<DPUSH	TP* (AB)>
	<PUSHJ	P* IMADMAN-PAGE-GIVE2>
	<JRST	FINIS>

ONEARG	<PUSHJ	P* IMADMAN-PAGE-GIVE1>
	<JRST	FINIS>

<INTERNAL-ENTRY IMADMAN-PAGE-GIVE1 1>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [<1>]>
	<JRST	PG1>

<INTERNAL-ENTRY IMADMAN-PAGE-GIVE2 2>
	<SUBM	M* (P)>
PG1	<MOVE	A* (TP)>	;"YES, GET IT"
	<JUMPLE	A* WRONGT>
	<MOVE	B* (TP) -2>	;"GET NUMBER OF FIRST BLOCK"
	<JUMPL	B* WRONGT>
	<MOVEI	O* (B)>
	<ADDI	O* (A)>
	<CAIGE	B* 256>		;"VALID FIRST BLOCK NUMBER ?"
	<CAILE	O* 256>		;"VALID LAST+1 BLOCK NUMBER ?"
	<JRST	WRONGT>		;"NO, LOSE"
	<PUSHJ	P* PGGIVE>
	<MOVSI	A* <TYPE-CODE FIX>>
	<SUB	TP* [<4 (4)>]>
	<JRST	MPOPJ>

<END>

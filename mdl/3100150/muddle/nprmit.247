TITLE PRIMITIVE FUNCTIONS FOR MUDDLE
RELOCATABLE
.INSRT MUDDLE >

.GLOBAL IPLIST,ISPLIST

MFUNCTION MEMQ,SUBR
	;USE (AB)$,X: IN NDDT TO DEFINE X TO BE (AB)
	;USE 2(AB)<L: TO DEFINE L TO BE 2(AB)
	DEFINE X
		(AB) TERMIN
	DEFINE L
		2 (AB) TERMIN
	ENTRY 2
	HLRZ A, L
	PUSHJ P, SAT
	CAIE A, 2WORD
	JRST  MEMQ5
		;ITS A LIST
		;UINTREPEAT UNTIL LIST IS EXHAUSTED OR ELEMENT IS FOUND
	    MEMQ2:
		INTGO
		SKIPE B, L+1
		JRST MEMQ3
			;WE  REACHED NIL
			MOVSI A, TFALSE
			JRST FINIS
	    MEMQ3:
		;TRY AGAIN
		HLLZ E, (B)	;THE TYPE OF THE CAR
		MOVE C, X
		MOVE D, X+1
		CAME C, E
		JRST MEMQ4
			;THE TYPE IS THE SAME
			CAME D, 1 (B)
			JRST MEMQ4
				;THE VALUE IS EQ
				MOVSI A, TLIST
				JRST FINIS
	    MEMQ4:
		HRRZ B, (B)	;CHOP B
		MOVEM B, L+1
		JRST MEMQ2
    MEMQ5:
	CAIN A, 2NWORD
	JRST MEMQ9
	CAIE A, BASE
	JRST MEMQ1
	;ITS A VECTOR
	    MEMQ9:
		MOVE C, X
		MOVE D, X+1
		MOVE B, L+1
	;REPEAT UNTIL THE VECTOR IS EXHAUSTED OR THE ELEMENT IS FOUND
	    MEMQ6:
		JUMPL B, MEMQ7
			MOVSI A, TFALSE
			MOVEI B, 0
			JRST FINIS
	    MEMQ7:
		CAME C, (B)
		JRST MEMQ8
			CAME D, 1 (B)
			JRST MEMQ8
				MOVSI A, TVEC
				JRST FINIS
	    MEMQ8:
		ADD B, [2,, 2]
		JRST MEMQ6
MEMQ1:
	;THE 2ND ARG IS NOT A LIST OR A VECTOR
	PUSH AP, $TATOM
	PUSH AP, MQUOTE MEMQ
	PUSH AP, L
	PUSH AP, L+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X,L

MFUNCTION MEMBER,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE L
		2 (AB) TERMIN
	MOVEI E, 0
	JRST MEMB0
MFUNCTION FIRSTMEM,SUBR
	MOVEI E, 1
    MEMB0:
	ENTRY 2
	HLRZ A,L
	CAIE A, TATOM
	JRST MEMB14
		MOVE A, L+1
		PUSHJ P, IPLIST
		MOVEM A, L+1
		MOVSI A, TLIST
		MOVEM A, L
    MEMB14:
	HLRZ A, L
	PUSHJ P, SAT
	CAIE A, 2WORD
	JRST  MEMB5
		;ITS A LIST
		;UREPEAT UNTIL LIST IS EXHAUSTED OR ELEMENT IS FOUND
	    MEMB2:
		SKIPE B, L+1
		JRST MEMB3
			;WE  REACHED NIL
			MOVSI A, TFALSE
			JRST FINIS
	    MEMB3:
		;TRY AGAIN
		PUSH AP, X
		PUSH AP, X+1
		JUMPN E, MEMB10
			HLLZ C, (B)
			PUSH AP, C
			PUSH AP, 1(B)
			JRST MEMB11
		    MEMB10:
			HLLZ C, (B)
			PUSH AP, C
			PUSH AP, 1(B)
			PUSH AP, $TFIX
			PUSH AP, [1]
			MCALL 2,GET
			PUSH AP, A
			PUSH AP, B
	    MEMB11:
		MCALL 2,EQUAL
		SKIPN B
		JRST MEMB8
			MOVSI A, TLIST
			MOVE B, L+1
			JRST FINIS
	    MEMB8:
		MOVE B, L+1
		HRRZ B, (B)
		MOVEM B, L+1
		JRST MEMB2
    MEMB5:
	CAIN A, 2NWORD
	JRST MEMB6
	CAIE A, BASE
	JRST MEMB1
	;ITS A VECTOR
	;UREPEAT UNTIL THE VECTOR IS EXHAUSTED OR THE ELEMENT IS FOUND
	    MEMB6:
		SKIPGE B, L+1
		JRST MEMB7
			MOVSI A, TFALSE
			MOVEI B,0
			JRST FINIS
	    MEMB7:
		PUSH AP, X
		PUSH AP, X+1
		JUMPN E, MEMB12
			PUSH AP, 0(B)
			PUSH AP, 1(B)
			JRST MEMB13
		    MEMB12:
			PUSH AP, 0(B)
			PUSH AP, 1(B)
			PUSH AP, $TFIX
			PUSH AP, [1]
			MCALL 2,GET
			PUSH AP, A
			PUSH AP, B
	    MEMB13:
		MCALL 2,EQUAL
		SKIPN B
		JRST MEMB9
			MOVSI A, TVEC
			MOVE B, L+1
			JRST FINIS
	    MEMB9:
		MOVE B, [2,,2]
		ADDM B, L+1
		JRST MEMB6
    MEMB1:
	;THE 2ND ARG IS NOT A LIST OR A VECTOR
	PUSH AP, $TATOM
	PUSH AP, MQUOTE MEMBER
	PUSH AP, L
	PUSH AP, L+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X,L


MFUNCTION EQ,SUBR,[==?]
	DEFINE X
		(AB) TERMIN
	DEFINE Y
		2 (AB) TERMIN
	ENTRY 2
	MOVE A, X
	CAME A, Y
	JRST EQ1
		MOVE B, X+1
		CAME B, Y+1
		JRST EQ1
		MOVSI A, TATOM
		MOVE B, MQUOTE T
		JRST FINIS
EQ1:
	MOVSI A, TFALSE
	MOVEI B, 0
	JRST FINIS
	EXPUNGE X,Y

MFUNCTION LENGTH,SUBR
	DEFINE X
		(AB) TERMIN
	ENTRY 1
	HLRZ A, X
	PUSHJ P, SAT
	CAIE A, 2WORD
	JRST LENG1
	;ITS A LIST
		MOVEI B, 0	;INITIALIZE COUNT
	;UINTREPEAT UNTIL LENGTH IS FOUND
	     LENG2:
		INTGO
		SKIPE D, X+1
		JRST LENG3
			MOVSI A, TFIX
			JRST FINIS
	    LENG3:
		HRRZ D, (D)
		MOVEM D, X+1
		AOJA B,LENG2
    LENG1:
	CAIN A, 2NWORD
	JRST LENG5
	CAIE A, BASE
	JRST LENG4
	    LENG5:
		HLRE B, X+1
		MOVNS B
		ASH B,-1
		MOVSI A, TFIX
		JRST FINIS
    LENG4:
	;THE ARG IS NOT A LIST OR A VECTOR
	PUSH AP, $TATOM
	PUSH AP, MQUOTE LENGTH
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X

MFUNCTION TOP,SUBR
	DEFINE X
		(AB) TERMIN
	HLRZ A, X
	PUSHJ P, SAT
	CAIE A, 2NWORD
	JRST TOP1
		MOVE A, X
		MOVE B, X+1
		HLRE C, B	;NEG OF REMAINING LENGTH IN C
		SUB B, C	;RIGHT HALF OF B HAS ADDRESS OF HEADER
		HLRZ C, (B)	;C HAS LENGTH OF WHOLE VECTOR
		MOVN C, C	;C HAS NEG OF LENGTH
		ADDI B, 1(C)	;RIGHT HALF OF B IS CORRECT
		HRLI B, 1(C)	;LEFT HALF OF B IS CORRECT
		JRST FINIS
    TOP1:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE TOP
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X


MFUNCTION EMPTYP,SUBR,[EMPTY?]
	DEFINE X
		(AB) TERMIN
	ENTRY 1
	HLRZ A,X
	PUSHJ P, SAT
	CAIE A,2WORD
	JRST EMPTY2
		SKIPE X+1
		JRST EMPTY1
			MOVSI A,TATOM
			MOVE A,MQUOTE T
			JRST FINIS
    EMPTY2:
	CAIE A,2NWORD
	JRST EMPTY1
		SKIPGE X+1
		JRST EMPTY1
			MOVSI A,TATOM
			MOVE A,MQUOTE T
			JRST FINIS
    EMPTY1:
	MOVSI A,TFALSE
	MOVEI A,0
	JRST FINIS
	EXPUNGE X

MFUNCTION MONADP,SUBR,[MONAD?]
	DEFINE X
		(AB) TERMIN
	ENTRY 1
	HLRZ A, X
	CAIE A, TATOM
	JRST MONAD1
		MOVE A, X
		MOVE B, X+1
		JRST FINIS
    MONAD1:
	PUSHJ P, SAT
	CAIE A, 1WORD
	JRST MONAD2
		MOVE A, X
		MOVE B, X+1
		JRST FINIS
MONAD2:
	CAIE A, 2WORD
	JRST MONAD3
		SKIPE B, X+1
		JRST MONAD4
			MOVE A, X
			JRST FINIS
	    MONAD4:
		MOVSI A, TFALSE
		MOVEI B, 0
		JRST FINIS
  MONAD3:
	CAIE A, 2NWORD
	JRST MONAD5
		SKIPGE B, X+1
		JRST MONAD6
			MOVE A, X
			JRST FINIS
	    MONAD6:
		MOVSI A, TFALSE
		MOVEI B, 0
		JRST FINIS
    MONAD5:
	MOVE A,X
	MOVE B, X+1
	JRST FINIS
	EXPUNGE X

MFUNCTION ATOMP,SUBR,[ATOM?]
	DEFINE X
		(AB) TERMIN
	ENTRY 1
	HLRZ A, X
	CAIE A, TATOM
	JRST ATOMP1
		MOVE A, X
		MOVE B, X+1
		JRST FINIS
ATOMP1:
	MOVSI A, TFALSE
	MOVEI B, 0
	JRST FINIS
	EXPUNGE X


MFUNCTION QUOTE,FSUBR
	DEFINE X
		(AB) TERMIN
	ENTRY 1
	HLRZ A, X
	CAIE A, TLIST
	JRST QUOTE1
		SKIPN C, X+1
		JRST QUOTE2
		HLLZ A, (C)
		MOVE B, 1 (C)
		JRST FINIS
	    QUOTE2:
		PUSH AP, $TATOM
		PUSH AP, MQUOTE QUOTE
		PUSH AP, $TLIST
		PUSH AP, [0]
		MCALL 2,ERROR
		JRST FINIS
    QUOTE1:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE QUOTE
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X

MFUNCTION EQUAL,SUBR,[=?]
	DEFINE X
		(AB) TERMIN
	DEFINE Y
		2(AB) TERMIN
	ENTRY 2
	MOVE A, X
	CAME A, Y
	JRST EQUAL2
		MOVE B, X+1
		CAME B, Y+1
		JRST EQUAL1
			MOVSI A, TATOM
			MOVE B, MQUOTE T
			JRST FINIS
	    EQUAL2:
		MOVSI A, TFALSE
		MOVEI B, 0
		JRST FINIS
EQUAL1:
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, Y
	PUSH AP, Y+1
	HLRZ A, X
	PUSHJ P,UEQAL
	MOVSI A, TATOM
	MOVE B, MQUOTE T
	JRST FINIS
	EXPUNGE X,Y
IEQAL:
	DEFINE U
		-1(AP) TERMIN
	DEFINE V
		-3(AP) TERMIN
	HLRZ A, U
	HLRZ B, V
	CAME A, B
	JRST IEQAL2
		MOVE B, U+1
		CAME B, V+1
		JRST IEQAL1
			POPJ P,

	     IEQAL2:
		MOVSI A, TFALSE
		MOVEI B, 0
		JRST FINIS
    IEQAL1:
UEQAL:
	PUSHJ P, SAT
	CAIE A, 2WORD
	JRST UEQAL2
	;UINTREPEAT UNTIL LIST RUNS OUT
	    UEQAL3:
		INTGO
		;UOR
			SKIPE A, U+1
			JRST UEQAL0
				SKIPE B, V+1
				JRST UEQAL5
					POPJ P,
			    UEQAL0:
				SKIPE B, V+1
				JRST UEQAL4
		    UEQAL5:
			MOVSI A, TFALSE
			MOVEI B, 0
			JRST FINIS
	    UEQAL4:
		CAME A, B
		JRST UEQAL9
			POPJ P,
	    UEQAL9:
		MOVE C, (A)
		MOVE D, (B)
		HRRZM C, U+1
		HRRZM D, V+1
		HLLZ C, C
		HLLZ D, D
		PUSH AP, C
		PUSH AP, 1 (A)
		PUSH AP, D
		PUSH AP, 1 (B)
		PUSHJ P, IEQAL
		SUB AP, [4,,4]
		JRST UEQAL3
    UEQAL2:
	CAIE A, 2NWORD
	JRST UEQAL1
	;UINTREPEAT UNTIL VECTOR IS EXHAUSTED
	    UEQAL6:
		INTGO
		;UOR
			SKIPGE A, U+1
			JRST UEQAL.
				SKIPGE V+1
				JRST UEQAL8
					POPJ P,

			    UEQAL.:
				SKIPGE B, V+1
				JRST UEQAL7
		    UEQAL8:
			MOVSI A, TFALSE
			MOVEI B, 0
			JRST FINIS
	    UEQAL7:
		PUSH AP, (A)
		PUSH AP, 1 (A)
		PUSH AP, (B)
		PUSH AP, 1 (B)
		PUSHJ P, IEQAL
		SUB AP, [4,,4]
		MOVE A, [2,,2]
		MOVE B, [2,,2]
		ADDM A, U+1
		ADDM B, V+1
		JRST UEQAL6
UEQAL1:
	MOVSI A, TFALSE
	MOVEI B, 0
	JRST FINIS
	EXPUNGE U,V

MFUNCTION APPEND,SUBR
	DEFINE ANSWER
		 (TB) TERMIN
	DEFINE TAIL
		2 (TB) TERMIN
	DEFINE ARGS
		4 (TB) TERMIN
	DEFINE X
		6 (TB) TERMIN
	ENTRY
	PUSH TP, $TLIST
	PUSH TP, [0]
	PUSH TP, $TLIST
	PUSH TP, [0]
	PUSH TP, $TAB
	PUSH TP, AB
	PUSH TP, [0]
	PUSH TP, [0]
	MOVE E, AB
	;UINTREPEAT UNTIL ARGS EXHAUSTED
	    APPEN1:
		INTGO
		SKIPGE E, ARGS+1
		JRST APPEN2
			MOVSI A, TLIST
			MOVE B, ANSWER+1
			JRST FINIS
	    APPEN2:
		HLLZ C, (E)
		MOVEM C, X
		MOVE D, 1(E)
		MOVEM D, X+1
		CAMN C, $TLIST
		JRST APPEN3
			PUSH AP, $TATOM
			PUSH AP, MQUOTE APPEND
			PUSH AP, X
			PUSH AP, X+1
			PUSH AP, $TATOM
			PUSH AP, MQUOTE WRONG-TYPE
			MCALL 3,ERROR
			JRST FINIS
	    APPEN3:
		;UINTREPEAT UNTIL LIST X IS EXHAUSTED
		    APPEN4:
			INTGO
			SKIPE D, X+1
			JRST APPEN5
				JRST APPEN7
		    APPEN5:
			HLLZ A, (D)
			MOVE B, 1(D)
			HRRZ D, (D)
			MOVEM D, X+1
			PUSH AP, A
			PUSH AP, B
			MCALL 1,NCONS
			SKIPE D, TAIL+1
			JRST APPEN6
				MOVEM B, TAIL+1
				MOVEM B, ANSWER+1
				JRST APPEN4
			    APPEN6:
				HRRM B, (D)
				MOVEM B, TAIL+1
			JRST APPEN4
	    APPEN7:
		MOVE C, [2,,2]
		ADDM C, ARGS+1
		JRST APPEN1
	EXPUNGE ANSWER,TAIL,ARGS,X

MFUNCTION NTH,SUBR
	DEFINE I
		(AB) TERMIN
	DEFINE X
		2(AB) TERMIN
	ENTRY 2
	PUSH AP,X
	PUSH AP,X+1
	PUSH AP,I
	PUSH AP,I+1
	MCALL 2,GET
	JRST FINIS
	EXPUNGE X,I

MFUNCTION GET,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE I
		2(AB) TERMIN
	MOVEI E, 1	;IF E IS 1 THEN MUST TAKE THE CONTENTS
	JRST GET0

MFUNCTION AT,SUBR
	MOVEI E, 0

GET0:
	ENTRY 2
    GET2:
	HLRZ A, X
	PUSHJ P, SAT
	CAIE A, 2WORD
	JRST GET5
		HLRZ A,I
		CAIE A,TFIX
		JRST GET14
			;UINTREPEAT UNTIL COUNT EXHAUSTED
				DEFINE II
					4 (AB) TERMIN
				DEFINE XX
					6 (AB) TERMIN
				PUSH AP, I
				PUSH AP, I+1
				PUSH AP, X
				PUSH AP, X+1
			    GET3:
				SKIPE B, XX+1
				JRST GET4
					JRST GET9
			    GET4:
				SOSLE A, II+1
				JRST GET8
					SKIPN E
					JRST GET12
						HLLZ A, (B)
						MOVE B, 1(B)
						JRST FINIS
					    GET12:
						MOVSI A, TLOCL
						JRST FINIS
			    GET8:
				MOVE B, (B)
				HRRZM B, XX+1
				JRST GET3
				EXPUNGE II,XX
	    GET14:	;BEWARE VECTORS ALSO USE THIS CODE!
		CAIE A,TATOM
		JRST GET10
			PUSH AP,X
			PUSH AP,X+1
			MCALL 1,REST
			JRST FINIS
    GET5:
	CAIN A, 2NWORD
	JRST GET11
	CAIE A, BASE
	JRST GET10
	    GET11:
		HLRZ A,I
		CAIE A,TFIX
		JRST GET14
			MOVE C, I+1
			SOS C
			LSH C, 1
			HRL C, C
			ADD C, X+1
			JUMPL C, GET7
			JRST GET9
		    GET7:
			SKIPN E
			JRST GET13
				MOVE A, (C)
				MOVE B, 1(C)
				JRST FINIS
			    GET13:
				MOVSI A, TLOCV
				MOVE B, C
				JRST FINIS
    GET9:
	PUSH AP, $TATOM
	SKIPE E
	PUSH AP, MQUOTE GET
	PUSH AP, MQUOTE AT
	PUSH AP, I
	PUSH AP, I+1
	PUSH AP, X
	PUSH AP, X+1
	MCALL 3,ERROR
	JRST FINIS
    GET10:
	PUSH AP, $TATOM
	SKIPE E
	PUSH AP, MQUOTE GET
	PUSH AP, MQUOTE AT
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, I
	PUSH AP, I+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 4,ERROR
	JRST FINIS
	EXPUNGE I,X

MFUNCTION REST,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE  N
		2(AB) TERMIN
	ENTRY
	JUMPL AB, REST1
		PUSH AP, $TATOM
		PUSH AP, MQUOTE REST
		PUSH AP, $TATOM
		PUSH AP, MQUOTE NO-ARGS
		MCALL 2,ERROR
		JRST FINIS
    REST1:
	CAMG AB, [-2,,0]
	JRST REST12
		PUSH AP, $TFIX
		PUSH AP, [1]
		SUB AB, [-2,,0]
    REST12:
	CAMLE AB, [-6,,0]
	JRST REST2
		PUSH AP, $TATOM
		PUSH AP, MQUOTE REST
		PUSH AP, $TATOM
		PUSH AP, MQUOTE TOO-MANY-ARGS
		MCALL 2,ERROR
		JRST FINIS
    REST2:
	HLRZ A, N
	CAIN A, TFIX
	JRST REST3
		JRST REST13
    REST3:
	HLRZ A, X
	PUSHJ P, SAT
	CAIE A, 2WORD
	JRST REST4
	;UINTREPEAT UNTIL THE COUNT IS EXHAUSTED
		DEFINE XX
			4(AB) TERMIN
		DEFINE NN
			6(AB) TERMIN
		PUSH AP, X
		PUSH AP, X+1
		PUSH AP, N
		PUSH AP, N+1
	    REST9:
		SOSL B, NN+1
		JRST REST10
			MOVSI A, TLIST
			MOVE B, XX+1
			JRST FINIS
	    REST10:
		SKIPE B, XX+1
		JRST REST11
			JRST REST14
	    REST11:
		MOVE B, (B)
		HRRZM B, XX+1
		JRST REST9
		EXPUNGE XX,NN
    REST4:
	CAIN A, 2NWORD
	JRST REST15
	CAIE A, BASE
	JRST REST13
	    REST15:
		MOVE B, N+1
		JUMPL B, REST14
		LSH B, 1
		HRL B, B
		ADD B, X+1
		CAMGE B, [1,,0]
		JRST REST7
			JRST REST14
	    REST7:
		MOVSI A, TVEC
		JRST FINIS
    REST13:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE REST
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, N
	PUSH AP, N+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 4,ERROR
	JRST FINIS
    REST14:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE REST
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, N
	PUSH AP, N+1
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X,N

MFUNCTION BACK,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE  N
		2(AB) TERMIN
	ENTRY
	JUMPL AB, BACK1
		PUSH AP, $TATOM
		PUSH AP, MQUOTE BACK
		PUSH AP, $TATOM
		PUSH AP, MQUOTE NO-ARGS
		MCALL 2,ERROR
		JRST FINIS
    BACK1:
	CAMG AB, [-2,,0]
	JRST BACK12
		PUSH AP, $TFIX
		PUSH AP, [1]
		SUB AB, [-2,,0]
    BACK12:
	CAMLE AB, [-6,,0]
	JRST BACK2
		PUSH AP, $TATOM
		PUSH AP, MQUOTE BACK
		PUSH AP, $TATOM
		PUSH AP, MQUOTE TOO-MANY-ARGS
		MCALL 2,ERROR
		JRST FINIS
    BACK2:
	HLRZ A, N
	CAIN A, TFIX
	JRST BACK3
		JRST BACK13
    BACK3:
	HLRZ A, X
	PUSHJ P, SAT
	CAIN A, 2NWORD
	JRST BACK15
	CAIE A, BASE
	JRST BACK13
	    BACK15:
		MOVE C, N+1
		JUMPL C, BACK14
		LSH C, 1
		HRL C, C
		MOVE B, X+1
		SUB B, C	;THE ANSWER IS IN B
		HLRE D, B
		MOVN D, D	;LENGTH OF BACKED UP VECTOR IS IN D
		HLRE C, X+1
		MOVN C, C
		ADD C, X+1	;ADDRESS OF HEADER IS RIGHT HALF OF C
		HLRZ C, 0(C)	;TOTAL LENGTH OF THE VECTOR IS IN C
		CAMLE D, C
		JRST BACK14
			MOVSI A, TVEC
			JRST FINIS
    BACK13:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE BACK
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, N
	PUSH AP, N+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 4,ERROR
	JRST FINIS
    BACK14:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE BACK
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, N
	PUSH AP, N+1
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X,N

MFUNCTION IN,SUBR
	DEFINE X
		(AB) TERMIN
	ENTRY 1
	HLRZ A,X
	;UOR
		CAIN A, TLOCS
		JRST IN1

		CAIN A,TLOCI
		JRST IN1

		CAIE A, TLOCV
		JRST IN2
		    IN1:
			MOVE C, X+1
			MOVE A, (C)
			MOVE B, 1(C)
			JRST FINIS
    IN2:
	CAIE C, TLOCL
	JRST IN3
		MOVE C, X+1
		HLLZ A, (C)
		MOVE B, 1(C)
		JRST FINIS
    IN3:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE IN
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X

MFUNCTION SETLOC,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE Y
		2(AB) TERMIN
	ENTRY 2
	HLRZ C, X
	;UOR
		CAIN C, TLOCL
		JRST SETLOL

		CAIE C, TLOCS
		CAIN C, TLOCV
		JRST SETLOV
		JRST SETLO2
	SETLOL:
		SKIPA A,Y
	SETLOV:
		SKIPA A,Y
		HRR A,@X+1
		MOVE C,X+1
		MOVEM A,(C)
		MOVE B,Y+1
		MOVEM B, 1(C)
		JRST FINIS
    SETLO2:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE SETLOC
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, Y
	PUSH AP, Y+1
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X,Y

MFUNCTION PUT,FSUBR
	DEFINE ARGS
		(AB) TERMIN
	DEFINE N
		2(AB) TERMIN
	ENTRY
	MOVE D,ARGS+1
	JUMPN D, PUT4
		PUSH AP, $TATOM
		PUSH AP, MQUOTE NO-ARGS
		PUSH AP, $TATOM
		PUSH AP, MQUOTE PUT
		MCALL 2, ERROR
		JRST FINIS
    PUT4:
	HLLZ A,(D)
	PUSH AP,A
	PUSH AP,1(D)	
	HRRZ D,(D)
	MOVEM D,ARGS+1
	MCALL 1,EVAL
	PUSH AP,A	;INITIALIZE N
	PUSH AP,B
	;REPEAT UNTIL ARGS RUN OUT
		DEFINE PAIR
			4(AB) TERMIN
	    PUT5:
		SKIPE D,ARGS+1
		JRST PUT6
			MOVE A,N
			MOVE B,N+1
			JRST FINIS
	    PUT6:
		HLLZ A,(D)
		MOVE B,1(D)
		HRRZ D,(D)
		MOVEM D,ARGS+1
		CAME A,$TLMNT
		JRST PUT7
			PUSH AP,A
			PUSH AP,B
			MCALL 2,EVAL
	    PUT7:
		PUSH AP,A
		PUSH AP,B
		PUSH AP,A
		PUSH AP,B
		PUSH AP,$TFIX
		PUSH AP,[1]
		MCALL 1,GET
		CAME A,$TLMNT
		JRST PUT8
			PUSH AP,A
			PUSH AP,B
			MCALL 2,EVAL
	    PUT8:
		PUSH AP,PAIR
		PUSH AP,PAIR+1
		MCALL 1,REST
		PUSH AP, A
		PUSH AP,B
		MCALL 1,EMPTYP
		CAMN A,TFALSE
		JRST PUT10
			MCALL 2,PUT1
			JRST PUT5
	    PUT10:
		PUSH AP,$TFIX
		PUSH AP,[2]
		MCALL 2,GET
		CAME A,$TLMNT
		JRST PUT9
			PUSH AP,A
			PUSH AP,B
			MCALL 2,EVAL
	    PUT9:
		PUSH AP,A
		PUSH AP,B
		MCALL 3,PUT1
		JRST PUT5
	EXPUNGE L,ARGS,N

MFUNCTION PUT1,SUBR
	DEFINE N
		(AB) TERMIN
	DEFINE I
		2(AB) TERMIN
	DEFINE V
		4(AB) TERMIN
	ENTRY
	CAMG AP,[-1,,0]
	JRST PUT11
		PUSH AP,$TATOM
		PUSH AP,MQUOTE PUT1
		PUSH AP, $TATOM
		PUSH AP, MQUOTE TOO-FEW-ARGS
		MCALL 2,ERROR
		JRST FINIS
    PUT11:
	CAMG AP,[-2,,0]
	JRST PUT13
		PUSH AP,N
		PUSH AP,N+1
		PUSH AP,I
		PUSH AP,I+1
		MCALL 2,REMOVE1
		JRST FINIS
    PUT13:
	HLRZ A,N
	PUSHJ P,SAT
	CAIE A,2WORD
	JRST PUT12
		HLRZ C,I
		CAIE C,TFIX
		JRST PUT14
			PUSH AP,N
			PUSH AP,N+1
			PUSH AP,I
			PUSH AP,I+1
			MCALL 2,AT
			PUSH AP,A
			PUSH AP,B
			PUSH AP,V
			PUSH AP,V+1
			MCALL 2,SETLOC
			MOVE A,N
			MOVE B,N+1
			JRST FINIS
	    PUT14:
		CAIE A,TATOM
		JRST PUT3
			MOVE B,MQUOTE REST
			CAME B,I+1
			JRST PUT3
				PUSH AP,N
				PUSH AP,N+1
				PUSH AP,V
				PUSH AP,V+1
				MCALL 2,RPLACD
				MOVE A,N
				MOVE B,N+1
				JRST FINIS
    PUT12:
	CAIE A,2NWORD
	JRST PUT15
		HLRZ A,I
		CAIE A,TFIX
		JRST PUT3
			PUSH AP,N
			PUSH AP,N+1
			PUSH AP,I
			PUSH AP,I+1
			MCALL 2,AT
			PUSH AP,A
			PUSH AP,B
			PUSH AP,V
			PUSH AP,V+1
			MCALL 2,SETLOC
			MOVE A,N
			MOVE B,N+1
			JRST FINIS
    PUT15:
    PUT3:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE PUT1
	PUSH AP, N
	PUSH AP, N+1
	PUSH AP, I
	PUSH AP, I+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 4,ERROR
	JRST FINIS
	EXPUNGE N,I,V


MFUNCTION SASSOC,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE I
		2(AB) TERMIN
	ENTRY 2
	PUSH AP, I
	PUSH AP, I+1
	PUSH AP, X
	PUSH AP, X+1
	MCALL 2,FIRSTMEM
	PUSH AP, A
	PUSH AP, B
	PUSH AP, $TFIX
	PUSH AP, [1]
	MCALL 2,GET
	JRST FINIS
	EXPUNGE I,X


MFUNCTION RPLACD,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE Y
		2(AB) TERMIN
	ENTRY 2
	HLRZ A, X
	HLRZ C, Y
	;UAND
			CAIE A, TLIST
			JRST RPLCD1

			CAIE C, TLIST
			JRST RPLCD1

		MOVSI A, TLIST
		MOVE B, X+1
		MOVE D, Y+1
		HRRM D, (B)
		JRST FINIS
    RPLCD1:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE RPLACD
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, Y
	PUSH AP, Y+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 4,ERROR
	JRST FINIS
	EXPUNGE X,Y

RANDOM:	;THIS WILL RETURN A RANDOM 18. BITS IN A
	MOVEI A,5
	IMULM A,RANUM
	HLRZS A
	POPJ P,
RANUM:	5*5*5*5*5*5*5*5*5*5*5*5*5	;5 ^ 13.

END
D, D	;LENGTH OF BACKED UP VECTOR IS IN D
		HLRE C, X+1
		MOVN C, C
		ADD C, X+1	;ADDRESS OF HEADER IS RIGHT HALF OF C
		HLRZ C, 0(C)	;TOTAL LENGTH OF THE VECTOR IS IN C
		CAMLE D, C
		JRST BACK14
			MOVSI A, TVEC
			JRST FINIS
    BACK13:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE BACK
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, N
	PUSH AP, N+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 4,ERROR
	JRST FINIS
    BACK14:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE BACK
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, N
	PUSH AP, N+1
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X,N

MFUNCTION IN,SUBR
	DEFINE X
		(AB) TERMIN
	ENTRY 1
	HLRZ A,X
	;UOR
		CAIN A, TLOCS
		JRST IN1

		CAIN A,TLOCI
		JRST IN1

		CAIE A, TLOCV
		JRST IN2
		    IN1:
			MOVE C, X+1
			MOVE A, (C)
			MOVE B, 1(C)
			JRST FINIS
    IN2:
	CAIE C, TLOCL
	JRST IN3
		MOVE C, X+1
		HLLZ A, (C)
		MOVE B, 1(C)
		JRST FINIS
    IN3:
	PUSH AP, $TATOM
	PUSH AP, MQUOTE IN
	PUSH AP, X
	PUSH AP, X+1
	PUSH AP, $TATOM
	PUSH AP, MQUOTE WRONG-TYPE
	MCALL 3,ERROR
	JRST FINIS
	EXPUNGE X

MFUNCTION SETLOC,SUBR
	DEFINE X
		(AB) TERMIN
	DEFINE Y
		2(AB) TERMIN
	ENTRY 2
	HLRZ C, X
	;UOR
		CAIN C, TLOCL
		JRST SETLOL

		CAIE C, TLOCS
		CAIN C, TLOCV
		JRST SETLOV
		JRST SETLO2
	SETLOL:
		SKIPA A,Y
	SETLOV:
		SK
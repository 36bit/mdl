TITLE TENEX MUDDLE BOOTSTRAP

.INSRT MUDSYS;STENEX >

IF1,[
PRINTC /TYPE IN SUBSYSTEM NAME: /
.TTYMAC A
NAME=SIXBIT /A/
TERMIN
PRINTC /TYPE IN MUDDLE, MUDnm OR NMUDDLE: /
.TTYMAC A
DEFINE STREW
	ASCIZ /!A!/
	TERMIN
TERMIN
]
A=1
B=2
C=3

LOC 200

MUD:	MOVEI A,TABLE
	HRROI B,MUDSTR	;subsys name
	GTJFN		;GROK
	JRST GJLOSS	;no file?
	HRLI A,400000	;this fork
	MOVEM A,STUFF+1
	MOVSI 17,STUFF
	BLT 17,17
	JRST B		;DISCORPORATE

GJLOSS:	MOVE	B,A
	HRROI	C,ERRS
ERJOIN:	MOVEI	A,15	;CR
	PBOUT
	MOVEI	A,12	;LF
	PBOUT
	MOVE	A,C
	PSOUT
	HRLI	B,400000	;ME
	MOVEI	A,-1	;TTY OUTPUT
	SETZ	C,
	ERSTR
	HALTF

CLZLOS:	SETOM	B
	HRROI	C,[ASCIZ /SORRY: /]
	JRST	ERJOIN



ERRS: ASCII /Can't open file:    /
MUDSTR: STREW


TABLE:	0	;FLAGS
	377777,,377777	;INJFN,OUTJFN
	-1,,[ASCIZ /DSK/]	;DEFAULTS (DEV)
	-1,,[ASCIZ /MUDDLE/]	; (DIR)
	-1,,[ASCIZ /NMUDDLE/]	; (FN)
	-1,,[ASCIZ /SAV/]		; (EXT)
	0
	0
	0
	
STUFF:	OFFSET -.
	NAME
	0	;GETS JFN
	GET	;LOADS
	MOVEI 1,400000
	GEVEC
	MOVNI A,1
	CLOSF
	JRST CLZLOS
	RLJFN	;CLOSE & RELEASE ALL FILES
	JRST CLZLOS
	JRST 1(2)
	OFFSET 0

END MUD

	TITLE RUNCMP

.DECSAV

O=0
A=1
B=2
C=3
D=4

P=17

LOC 140

PCLEV1:	0
LEVTAB:	0,,PCLEV1
	0
	0
CHNTAB:	BLOCK	19.
	1,,INFINT
	BLOCK	16.
INFHDL:	0
PDL:	BLOCK	10

START:	RESET
	MOVE	P,[-10,,PDL]
	MOVEI	A,.FHSLF
	MOVE	B,[LEVTAB,,CHNTAB]
	SIR
	EIR
	MOVE	B,[<1_<35.-.ICIFT>>]
	AIC
	MOVE	A,[GJ%SHT\GJ%OLD]
	HRROI	B,[ASCIZ /SYS:NPCOMP.EXE/]
	GTJFN
	 JRST	STRTER
	PUSH	P,A
	HRLZI	A,(CR%CAP)
	CFORK
	 JRST	STRTER
	MOVEM	A,INFHDL
	HRLS	A
	HRR	A,(P)
	GET
	MOVE	A,INFHDL
	MOVEI	B,0
	SFRKV
	HRROI	A,[ASCIZ /Compiler started.
/]
	PSOUT
WLOOP:	MOVE	A,[300.*1000.]
	DISMS
	MOVE	A,INFHDL
	RUNTM			; READ RUNTIME
	IDIV	A,B		; CONVERT TO SECONDS
	CAIG	A,7200.		; PAST TWO HOURS?
	 JRST	WLOOP		; NO
	MOVEI	A,.FHSLF
	DIR
	MOVE	A,INFHDL
	HFORK
	CLZFF
	HRROI	A,[ASCIZ /Compiler ran overtime.
/]
	ESOUT
	HALTF
	JRST	START
STRTER:	HRROI	A,[ASCIZ /Couldn't load compiler.
/]
	ESOUT
	HALTF
	JRST	START

INFINT:	MOVE	A,INFHDL
	CLZFF
	HRROI	A,[ASCIZ /Compiler halted.
/]
	PSOUT
	HALTF
	JRST	START
	END	START

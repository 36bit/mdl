<PACKAGE "FILINF">

<ENTRY DUMPED? CREATION-DATE>

<TITLE DUMPED?>
	<DECLARE ("VALUE" <OR ATOM FALSE> CHANNEL "OPTIONAL" <OR ATOM FALSE>)>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<ADD	AB* [<2(2)>]>
	<JUMPGE	AB* TAG1>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<PUSHJ	P* IDUMPED2>
	<JRST	FINIS>
TAG1	<PUSHJ	P* IDUMPED1>
	<JRST	FINIS>

<INTERNAL-ENTRY IDUMPED1 1>
	<SUBM	M* (P)>
	<PUSH	TP* <TYPE-WORD UNBOUND>>
	<PUSH	TP* [<-1>]>
	<JRST	DUMENT>

<INTERNAL-ENTRY IDUMPED2 2>
	<SUBM	M* (P)>
DUMENT	<MOVE	A* -2(TP)>
<IFOPSYS
(ITS	<MOVE	B* (A) 21>	; "status slot"
	<ANDI	B* *77*>
	<CAIE	B* *43*>
	 <JRST	FAIL>)
(TOPS20)>
	<GETYP	B* -1(TP)>
	<CAIE	B* <TYPE-CODE UNBOUND>>
	 <JRST	SETDMP>
<IFOPSYS
(ITS	<*CALL	DUMPED>
	 <JRST	FAIL>)
(TOPS20	<MOVE	A* 1(A)>
	<MOVE	B* [<(1) *17*>]>
	<MOVEI	C* 1(P)>
	<GTFDB>
	<MOVE	B* 1(P)>)>
	<JUMPE	B* FAIL>
RETRUE	<MOVSI	A* <TYPE-CODE ATOM>>
	<MOVE	B* <MQUOTE T>>
FEXIT	<SUB	TP* [<4(4)>]>
	<JRST	MPOPJ>

SETDMP
<IFOPSYS
(ITS	<MOVEI	C* 1>
	<CAIN	B* <TYPE-CODE FALSE>>
	 <MOVEI	C* 0>
      	<*CALL	SDUMPD>
	 <JRST	FAIL>
	<JRST	RETRUE>)
(TOPS20	<MOVE	C* [<(1) 1>]>
	<CAIN	B* <TYPE-CODE FALSE>>
	 <MOVEI	C* 0>
	<MOVE	A* 1(A)>
	<HRLI	A* *400017*>
	<MOVNI	B* -1>
	<CHFDB>
	 <ERJMP	FAIL>
	<JRST	RETRUE>)>

FAIL	<MOVSI	A* TFALSE>
	<SETZ	B*>
	<JRST	FEXIT>

<IFOPSYS
(ITS
DUMPED	<SETZ>
	<SIXBIT "RDMPBT">
	<(A) 1>			; "io channel #"
	<SETZM B>

SDUMPD	<SETZ>
	<SIXBIT "SDMPBT">
	<(A) 1>
	<SETZ C>)>		; "new frob"

<SUB-ENTRY CREATION-DATE #DECL ("VALUE" FIX CHANNEL)>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<PUSHJ	P* ICREATION>
	<JRST	FINIS>

<INTERNAL-ENTRY ICREATION 1>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
<IFOPSYS
(ITS	<*CALL	[<SETZ> <SIXBIT "RFDATE"> <MOVE 1(A)> <SETZM B>]>
	 <SETZ	B*>)
(TOPS20	<MOVE	A* 1(A)>
	<MOVEI	B* 1(P)>
	<MOVEI	C* 2>
	<RFTAD>
	<SKIPG	B* 1(B)>
	 <SETZ	B*>)>
	<MOVSI	A* <TYPE-CODE FIX>>
	<JRST	MPOPJ>

<END>

<ENDPACKAGE>

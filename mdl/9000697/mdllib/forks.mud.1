<TITLE CREATE>
	<PSEUDO <SETG CR-CAP *200000000000*>>
	<PSEUDO <SETG GJ-SHT *1000000*>>
	<PSEUDO <SETG GJ-OLD *100000000000*>>

	<DECLARE ("VALUE" <OR FALSE FIX>
		  STRING
		  "OPTIONAL"
		  <OR FALSE STRING>
		  FIX)>

	<HLRE	C* AB>
	<PUSH	TP* (AB)>
	<AOBJN	AB* HERE -1>
	<ASH	C* -1>
	<ADDI	C* CFPUSH 1>
	<PUSHJ	P* @ (C)>
	<JRST	FINIS>

	<ICFORK3>
	<ICFORK2>
CFPUSH	<ICFORK1>

<INTERNAL-ENTRY ICFORK1 1>
	<PUSH	TP* <TYPE-WORD FALSE>>
	<PUSH	TP* [<0>]>
	<JRST	ICFORA2>

<INTERNAL-ENTRY ICFORK2 2>
ICFORA2	<PUSH	TP* <TYPE-WORD FIX>>
	<PUSH	TP* [<0>]>
	<JRST	ICFORA3>

<INTERNAL-ENTRY ICFORK3 3>
ICFORA3	<SUBM	M* (P)>
	<HRRZ	C* -5(TP)>		; "SIZE OF STRING"
	<ADDI	C* 5>
	<IDIVI	C* 5>
	<MOVE	D* C>
	<HRL	D* D>
	<HRROI	B* 1(P)>
	<ADD	P* D>
	<MOVE	A* -4(TP)>
	<MOVE	C* B>
	<HRLI	C* *440700*>
	<HRRZ	E* -5(TP)>
	<ILDB	A>
	<IDPB	C>
	<SOJG	E* HERE -2>
	<MOVEI	O* 0>
	<IDPB	C>			; "MAKE SURE ASCIZ"
	<MOVSI	A* *100001*>		; "GJ%OLD+GJ%SHT"
	<GTJFN>				; "JFN FOR FILE"
	 <JRST GTLOSE>
	<SUB	P* D>
	<MOVE	E* A>
	<MOVSI	A* *200000*>		; "CR%CAP"
	<CFORK>				; "MAKE A FORK"
	 <JRST CFLOSE>			; "WHY?"
	<EXCH	A* E>			; "SAVE PROCESS HANDLE"
	<HRL	A* E>			; "HANDLE,,JFN"
	<GET-JSYS>			; "GET A JOB" 
	<MOVE	A* E>
	<SETO	B*>
	<SETZ	C*>
	<EPCAP>				; "WHY NOT?"
	<GETYP	O* -3(TP)>
	<CAIE	O* <TYPE-CODE STRING>>
	 <JRST	NOJCL>
	<HRRZ	C* -3(TP)>		; "SIZE OF STRING"
	<ADDI	C* 5>
	<IDIVI	C* 5>
	<MOVE	D* C>
	<HRL	D* D>
	<HRROI	B* 1(P)>
	<ADD	P* D>
	<MOVE	A* -2(TP)>
	<MOVE	C* B>
	<HRLI	C* *440700*>
	<HRRZ	F* -3(TP)>
	<ILDB	A>
	<IDPB	C>
	<SOJG	F* HERE -2>
	<MOVEI	O* 0>
	<IDPB	C>			; "MAKE SURE ASCIZ"
	<MOVE	A* B>
	<RSCAN>				; "PUT JCL IN BUFFER"
	 <JRST JCLOSE>
	<SUB	P* D>
NOJCL	<MOVE	A* E>			; "PROCESS HANDLE"
	<MOVE	B* (TP)>		; "ENTRY VECTOR LOC"
	<SFRKV>
	<WFORK>
	<MOVSI	A* <TYPE-CODE FIX>>
	<MOVE	B* E>
CFEXIT	<SUB	TP* [<6(6)>]>
	<JRST	MPOPJ>

JCLOSE
GTLOSE	<SUB	P* D>

CFLOSE	<MOVSI	A* <TYPE-CODE FALSE>>
	<MOVEI	B* 0>
	<JRST	CFEXIT>

<SUB-ENTRY KILL #DECL ("VALUE" FIX FIX)>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<PUSHJ	P* IKFORK>
	<JRST	FINIS>
<INTERNAL-ENTRY IKFORK 1>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<KFORK>
	<MOVE	A* -1(TP)>
	<MOVE	B* (TP)>
	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>

<SUB-ENTRY HALT #DECL ("VALUE" FIX FIX)>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<PUSHJ	P* IHFORK>
	<JRST	FINIS>
<INTERNAL-ENTRY IHFORK 1>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<HFORK>
	<MOVE	A* -1(TP)>
	<MOVE	B* (TP)>
	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>

<SUB-ENTRY START #DECL ("VALUE" FIX FIX)>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<PUSHJ	P* ISFORK>
	<JRST	FINIS>
<INTERNAL-ENTRY ISFORK 1>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<RFSTS>
	<JUMPGE	A* CONT>
	<MOVE	A* (TP)>
	<RFORK>
CONT	<MOVE	A* (TP)>
	<SFORK>
	<WFORK>
	<MOVE	A* -1(TP)>
	<MOVE	B* (TP)>
	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>

<SUB-ENTRY FREZE #DECL ("VALUE" FIX FIX)>
	<PUSH	TP* (AB)>
	<PUSH	TP* 1(AB)>
	<PUSHJ	P* IFFORK>
	<JRST	FINIS>
<INTERNAL-ENTRY IFFORK 1>
	<SUBM	M* (P)>
	<MOVE	A* (TP)>
	<FFORK>
	<MOVE	A* -1(TP)>
	<MOVE	B* (TP)>
	<SUB	TP* [<2(2)>]>
	<JRST	MPOPJ>

<END>
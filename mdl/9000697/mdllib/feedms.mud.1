<USE "TENXIO">

<SETG SCRATCH-STRING <ISTRING 5000>>


<DEFINE FEED-MESSAGES (INFILE OUTFILE MAXSLEEP "OPTIONAL" (PRTFLG T)
						"AUX" CHAN)
<RANDOM *765432123456* *525252636363*>
#DECL ((CHAN) <OR CHANNEL FALSE>)
<COND	(<SET CHAN <OPEN "READ" .INFILE>>
	<MAPF <> <FUNCTION ("AUX" STR SLPTIM)
		#DECL ((STR) <OR STRING FALSE> (SLPTIM) FIX)
		<COND	(<TYPE? <SET STR <GET-NEXT-MSG .CHAN>> STRING>
			<SET SLPTIM <MOD <RANDOM> .MAXSLEEP>>
			<AND	.PRTFLG
				<CRLF>
				<PRINC "Sleeping for ">
				<PRIN1 .SLPTIM>
				<PRINC " seconds.">>
			<SLEEP .SLPTIM>
			<AND	.PRTFLG
				<CRLF>
				<PRINC "Appending msg from location ">
				<PRIN1 <17 .CHAN>>
				<PRINC " of file, length = ">
				<PRIN1 <LENGTH .STR>>>
			<APPEND-TO-FILE .OUTFILE .STR>)
			(ELSE
			<AND	.PRTFLG
				<CRLF>
				<PRINC "Finished.">>
			<CLOSE .CHAN>
			<MAPLEAVE>)>>>)
	(<ERROR COULDNT-OPEN-FILE!-ERRORS .CHAN>)>>



<DEFINE GET-NEXT-MSG (CHAN "AUX" (STR ,SCRATCH-STRING)
					(SLNT <LENGTH .STR>)
					VAL TMP RLNT LNT FLNT MLNT ACC BEGACC CNT)
#DECL ((VALUE) <OR STRING FALSE>
	(CHAN) CHANNEL
	(STR SCR) STRING
	(VAL TMP) ANY
	(LNT FLNT) <OR FIX FALSE>
	(SLNT SCRLNT RLNT ACC BEGACC MLNT CNT) FIX)
<COND	(<AND	<TYPE? .CHAN CHANNEL>
		<G? <1 .CHAN> 0>
		<=? <2 .CHAN> "READ">
		<SET FLNT <FILE-LENGTH .CHAN>>
		<L? <SET BEGACC <17 .CHAN>> .FLNT>
		<TYPE? <SET LNT <READSTRING .STR .CHAN %<STRING <ASCII 31>> '<>>> FIX>
		<SET RLNT <- .SLNT .LNT>>
		<SET VAL <MEMQ !", .STR>>
		<SET TMP <MEMQ !"; .VAL>>
		<G? <LENGTH .VAL> .RLNT>
		<G? <LENGTH .TMP> .RLNT>
		<PROG ()
		<SET CNT 0>
		<MAPF <>
			<FUNCTION (CHR "AUX" (CHRNUM <ASCII .CHR>))
			#DECL ((CHR) CHARACTER (CHRNUM) FIX)
			<COND	(<AND	<G=? .CHRNUM %<ASCII !"0>>
					<L=? .CHRNUM %<ASCII !"9>>>
				<SET CNT <+ <* .CNT 10>
						<- .CHRNUM %<ASCII !"0>>>>)
				(<MAPLEAVE>)>> <REST .VAL>>
		T>
		<G? .CNT 0>
		<G=? .FLNT <SET ACC <+ 1 <17 .CHAN> .CNT>>>>
	<SET MLNT <- .ACC .BEGACC>>
	<COND	(<G=? .SLNT .MLNT>
		<SET STR <REST .STR <- .SLNT .MLNT>>>)
		(<SET STR <SETG SCRATCH-STRING <ISTRING .MLNT>>>)>
	<ACCESS .CHAN .BEGACC>
	<AND	<READSTRING .STR .CHAN .MLNT '<>>
		.STR>)>>



<DEFINE APPEND-TO-FILE (FILNAM STR "AUX" FILJFN)
<COND	(<AND	<SET FILJFN <OR	<GET-FILE .FILNAM 0>
				<GET-FILE .FILNAM *400000000000*>>>
		<SET FILJFN <OPEN-FILE .FILJFN *070000021000*>>>
	<PRINTS-FILE .FILJFN .STR>
	<CLOSE-FILE .FILJFN 0>)
	(<ERROR COULDNT-OPEN-FILE!-ERRORS .FILNAM .FILJFN>)>>





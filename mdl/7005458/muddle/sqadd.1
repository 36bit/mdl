
"ROUTINES TO ALLOW ADDITIONS OF SYMBOLS TO THE MUDDLE SYMBOL TABLE."

<SETG SQUOZE-DIR "MUDSAV">

<SETG SQUOZE-NM1 "SQUOZE">

<SETG DIRBUF <IUVECTOR 1024 0>>

<GDECL (DIRBUF) <UVECTOR [1024 FIX]>>

<DEFINE ADD-SQUOZE TOPL (FNAME SYMNAM SYMVAL
			 "OPTIONAL" (ICH
				     <OPEN "READB"
					   ,SQUOZE-NM1
					   <STRING "TAB" <UNPARSE ,MUDDLE>>
					   "DSK"
					   ,SQUOZE-DIR>)
			 "AUX" (OCH <OPEN "PRINTB" .FNAME>) PTR RNAME)
	#DECL ((FNAME) STRING (SYMNAM) ATOM (RNAME SYMVAL) FIX)
	<UNWIND <PROG ()
		      <COND (<NOT .ICH> <RETURN .ICH .TOPL>)
			    (<NOT .OCH> <RETURN .OCH .TOPL>)>
		      <READB ,DIRBUF .ICH>
		      <CLOSE .ICH>
		      <SET RNAME <CHTYPE <SQUOZE!-RSUBRS <SPNAME .SYMNAM>> FIX>>
		      <SET PTR ,DIRBUF>
		      <REPEAT ()
			      <COND (<L? .RNAME <1 .PTR>> <RETURN>)
				    (<EMPTY? .PTR> <RETURN>)
				    (<==? .RNAME <1 .PTR>>
				     <ERROR SYMBOL-THERE!-ERRORS>)>
			      <SET PTR <REST .PTR 2>>>
		      <COND (<==? <LENGTH .PTR> 1024>)
			    (ELSE
			     <PRINTB <SUBSTRUC ,DIRBUF 0 <- 1024 <LENGTH .PTR>>>
				     .OCH>)>
		      <PRINTB <UVECTOR .RNAME .SYMVAL> .OCH>
		      <PRINTB <SUBSTRUC .PTR 0 <- <LENGTH .PTR> 2>> .OCH>
		      <CLOSE .OCH>>
		<PROG ()
		      <COND (.ICH <CLOSE .ICH>)>
		      <COND (.OCH <CLOSE .OCH>)>>>>

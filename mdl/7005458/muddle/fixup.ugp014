<BLOCK (<ROOT>)>

RSUBRS

<ENDBLOCK>

<BLOCK (<MOBLIST RSUBRS 3> <ROOT>)>

<GDECL (MUDDLE) FIX>

<DEFINE FIXUP
	(BODY FIXUPS "EXTRA" (TCODE <1 .BODY>))
	#DECL ((BODY) <<PRIMTYPE VECTOR> CODE> (FIXUPS) <LIST FIX>
	       (TCODE) <CODE [REST FIX]>)
	<CHUTYPE .TCODE FIX>
	<REPEAT ((FIXES <REST .FIXUPS>) REF OFFSET SYM  PSYM TEMP)
	#DECL ((FIXES) LIST   (OFFSET) FIX
		(PSYM) ATOM)
		<AND <EMPTY? .FIXES> <RETURN FIXUP>>			;"Finished ?"
		<SET SYM <1 .FIXES>>
		<COND	(<TYPE? .SYM ATOM>		;"Lookup the proper symbol"
			 <COND (<==? <1 .FIXUPS> ,MUDDLE> <RETURN FIXUP>)
			       (<GASSIGNED? .SYM><SET REF ,.SYM>)
			       (<SET REF <SQUOTA <SQUOZE <SPNAME .SYM>>>>)
			       (<ERROR UNKNOWN-TYPE!-ERRORS .SYM>)>)
			(<TYPE? .SYM STRING>  <SET PSYM <PARSE .SYM>>
			 <SET REF <REPEAT () <COND
				(<SET TEMP <MEMQ .PSYM <ALLTYPES>>>
					<RETURN <- <LENGTH <ALLTYPES>> <LENGTH .TEMP>>>)
				(T <ERROR FIXUP UNKNOWN-TYPE!-ERRORS .PSYM> )>>>)
			(ELSE <ERROR FIXUP UNKNOWN-SYMBOL!-ERRORS .SYM>)>
		<SET OFFSET <- <CHTYPE .REF FIX> <2 .FIXES>>>
				;"Relative change between previous and current value ?"
		<PUT .FIXES 2 <CHTYPE .REF FIX>>
		<OR <0? .OFFSET> 
			<REPEAT ((USES <3 .FIXES>))		;"YES, fixup"
				#DECL ((USES) <LIST [REST FIX]>)
				<SET REF <1 .USES>>
				<COND
				(<G? .REF 0>
					<PUT .TCODE .REF <+ <NTH .TCODE .REF> .OFFSET>>)
				(<SET REF <- .REF>>
					<PUT .TCODE .REF <CHTYPE <PUTBITS
					<GETBITS <NTH .TCODE .REF>
						%<BITS 18>> %<BITS 18 18>
					<+ <CHTYPE <GETBITS <NTH .TCODE .REF> 
						%<BITS 18 18>> FIX> .OFFSET>> FIX>>)>
				<AND <EMPTY? <SET USES <REST .USES>>> <RETURN 0>>>>
		<SET FIXES <REST .FIXES 3>>>
	<PUT .FIXUPS 1 ,MUDDLE>
	<SET BODY <RSUBR .BODY>>
	<AND <ASSIGNED? KEEP-FIXUPS!->
	     .KEEP-FIXUPS!-	;"Save fixup info if in DEBUG mode"
		<PUT .BODY RSUBR .FIXUPS>>
	.BODY>


<DEFINE SQUOZE (STR
		"OPTIONAL" (HIBITS 0)
		"AUX" (MULF <* 40 40 40 40 40 40>) (VAL 0) (COUNT 6) C (TC 0))
	#DECL ((HIBITS) <PRIMTYPE WORD> (VAL COUNT TC MULF) FIX (C) CHARACTER
	       (STR) STRING)
	<REPEAT ()
		<COND (<OR <EMPTY? .STR> <L? .COUNT 1>>
		       <RETURN <CHTYPE <PUTBITS .VAL <BITS 4 32> .HIBITS>
				       WORD>>)>
		<SET TC <CHTYPE <SET C <1 .STR>> FIX>>
		<SET STR <REST .STR>>
		<COND (<AND <G=? .TC <ASCII !"0>> <L=? .TC <ASCII !"9>>>
		       <SET TC <- .TC 47>>)
		      (<AND <G=? .TC <ASCII !"A>> <L=? .TC <ASCII !"Z>>>
		       <SET TC <- .TC 54>>)
		      (<AND <G=? .TC <ASCII !"a>> <L=? .TC <ASCII !"z>>>
		       <SET TC <- .TC 86>>)
		      (<==? .C !".> <SET TC 37>)
		      (<==? .C !"$> <SET TC 38>)
		      (<==? .C !"%> <SET TC 39>)
		      (T <AGAIN>)>
		<SET COUNT <- .COUNT 1>>
		<SET VAL <+ .VAL <* .TC <SET MULF </ .MULF 40>>>>>>>


<ENDBLOCK>
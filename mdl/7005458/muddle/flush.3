
"ROUTINE TO FLUSH A VERSION OF MUDDLE"

<FLOAD "AR1:FLSHER NBIN MUDDLE;">

<SETG RCL ![0!]>

<SETG DIRBUF <IUVECTOR 1024 0>>

<GDECL (RCL DIRBUF) <UVECTOR [REST FIX]>>

<DEFINE FLUSH-IT (VERSION "AUX" CH NUM) 
	#DECL ((VERSION) FIX)
	<SET CH <OPEN "READB" "SAV" "FILE" "DSK" "MUDSAV">>
	<SETG DELCH <OPEN "PRINT" "DELETE" "SAVS" "DSK" "MUDTMP">>
	<COND (<NOT .CH> <ERROR .CH>)>
	<COND (<NOT ,DELCH> <ERROR ,DELCH>)>
	<READB ,RCL .CH>
	<SET NUM <1 ,RCL>>
	<NOSHUF>
	<REPEAT ((CNT 1) PNO)
		<ACCESS .CH .CNT>
		<READB ,RCL .CH>
		<SET PNO <1 ,RCL>>
		<ACCESS .CH <* .PNO 1024>>
		<READB ,DIRBUF .CH>
		<HACK-DIRECTORY ,DIRBUF .VERSION>
		<COND (<==? .CNT .NUM> <RETURN>)>
		<SET CNT <+ .CNT 1>>>
	<CLOSE ,DELCH>
	<CLOSE .CH>>

<DEFINE HACK-DIRECTORY (DIR VER "AUX" CNT) 
	#DECL ((DIR) <UVECTOR [REST FIX]> (VER) FIX)
	<SET CNT <1 .DIR>>
	<SET DIR <REST .DIR>>
	<REPEAT (NM1 VERS NAM)
		<SET NM1 <1 .DIR>>
		<SET VERS <CHTYPE <GETBITS <2 .DIR> ,VERSION-FD> FIX>>
		<COND (<==? .VERS .VER>
		       <SET NAM <6TOCHS .NM1>>
		       <PCODE .NAM 0>
		       <PC>
		       <PRINX .NAM ,DELCH>
		       <PRINC " SAV" ,DELCH>
		       <PRIN1 .VER ,DELCH>
		       <CRLF ,DELCH>)>
		<SET CNT <- .CNT 1>>
		<COND (<0? .CNT> <RETURN>)>
		<SET DIR <REST .DIR 2>>>>

<SETG VERSION-FD <BITS 10 18>>

<DEFINE PRINX (STR OCH) 
	#DECL ((STR) STRING (CH) CHANNEL)
	<MAPF <>
	      <FUNCTION (CH) 
		      <COND (<==? .CH !\ > <MAPLEAVE>)>
		      <PRINC .CH .OCH>>
	      .STR>
	T>

<DEFINE 6TOCHS (SIX "AUX" (STR <ISTRING 6>) (PTR 6) NUM) 
	<SET SIX <CHTYPE .SIX FIX>>
	<REPEAT ()
		<COND (<0? .PTR> <RETURN>)>
		<SET NUM <GETBITS .SIX <BITS 6 0>>>
		<PUT .STR .PTR <CHTYPE <+ <CHTYPE .NUM FIX> 32> CHARACTER>>
		<SET PTR <- .PTR 1>>
		<SET SIX <CHTYPE <GETBITS .SIX <BITS 30 6>> FIX>>>
	.STR>